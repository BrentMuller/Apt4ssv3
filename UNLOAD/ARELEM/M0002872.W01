*
C.....FORTRAN SUBROUTINE  ....TLAXIS         8/68                    PH
C
      SUBROUTINE TLAXIS(NUMBER,NPRD,PARAM)
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
C
C
C
C
      INCLUDE (TOTAL)
      INCLUDE (FXCOR)
      INCLUDE (ZNUMBR)
      INCLUDE (ISV)
      INCLUDE (IFXCOR)
      INCLUDE (ISHR18)
      INCLUDE (KNUMBR)
C
      INCLUDE (GLFLGS)
C
C...   COMMON BLOCK FOR AERR INFORMATION
C
      COMMON/AERCOM/IAERFG
C
C...  COMMON FOR >PLOTFT/GOLOFT<
C
      DIMENSION IPLGO(26),COSAL(10),SINAL(10),CUTY(10),CUTX(10),CUTL(10)
      DIMENSION ICSEG(10),NLIOCI(10)
      DIMENSION VTA(3)
C
      COMMON/PLGOL/IPLGO,JCSEG,NCSEG,ICSEG,NLIOCI,
     +             COSAL,SINAL,CUTY,CUTX,CUTL,VTA
C
      DIMENSION PARAM(9)
      CHARACTER*6 TEST,DS,HLAG
      DATA DS/'DS    '/,HLAG/'LAG   '/
C
C...     IF MULTAX HAS NOT BEEN SPECIFIED ISSUE WARNING DIAGNOSTIC
      IF (MULOUT .NE. 1) CALL AERR(-22300,'TLAXIS  ')
C
      JTLFLG(IPS)=0
      JTLFLG(IDS)=0
      IPRD=NPRD/100
      NPRD=NPRD-IPRD*100
      IAUTAG=0
      MOTDIR=0
      IF (NPRD.NE.9) RINGFG=.FALSE.
      IP2=2
    1 IF (NPRD.LT.5) IP2=IP2-1
C            1   2   3   4  5  6  7  8  9 10 11  12  13  14  15
      GOTO(201,202,203,204,72,15,50,20,10,75,30,212,213,214,215,
C           16  17  18  19  20  21  22
     *     216,217,218,218,220,220,222),NPRD
C
C     **TLAXIS/1**
C
   10 IF (PARAM(1).NE.Z1)GOTO 150
      MANTAX=K1
      GO TO 140
C
C     **TLAXIS/NORMPS**
C
15    MANTAX=K2
      GAMMA=K0
      GO TO 28
C
C     **TLAXIS/NORMDS**
C
20    MANTAX=K3
      GAMMA=PI
C     **SET VALUES FOR NUMAX,RA & HI FOR TLAXIS/NORMPS & TLAXIS/NORMDS**
28    NUMAX=K1
      ALP=Z0
      RA=Z0
      HI=Z0
      GO TO 110
C
C     **TLAXIS/PARLEL,1 OR TLAXIS/PARLEL,2 **
C
30    IF(PARAM(2).LE.Z0.OR.PARAM(2).GT.Z2)GO TO 150
      GAMMA=Z0
  31  II=7
C     **IF BETA IS GREATER THAN 0,SET RA&HI AT UPPER LINE SEGMENT**
      IF(CUTDAT(6).GT.Z1EM7)GO TO 40
C     **SET RA&HI AT INTERSECTION OF UPPER&LOWER LINE SEGMENTS**
      II=6
C     **IF CORNER RADIUS IS GREATER THAN 0,RA=XH&HI=YH**
      IF(CUTDAT(2).GT.Z0)II=5
C     **IF BETA IS NEGATIVE,SET RA&HI AT EXTREMITY OF CORNER CIRCLE**
      IF(CUTDAT(6).LT.Z0)GO TO 45
40    RA=RI(II)
      HI=TLHITE(II)
      GO TO 48
45    RA=CUTDAT(3)+CUTDAT(2)
      HI=CUTDAT(4)
48    MANTAX=K3
      IF(PARAM(2).EQ.Z1)MANTAX=K2
      NUMAX=IP2
      IF (NUMBER.EQ.9)GOTO 96
      ALP=Z0
      AX4(1)=Z0
      AX4(2)=Z0
      AX4(3)=Z0
      GO TO 110
C
C     **TLAXIS/V OR TLAXIS/I,J,K**
C
50    MANTAX=K1
      CALL TRANSM(TA,PARAM,3,3,0)
      CALL VNORM(TA,TA)
      GO TO 140
C
C     **TLAXIS/A,B,RA,HI **
C     **TLAXIS/PARLEL,1,RA,HI OR TLAXIS/PARLEL,2,RA,HI**
C     **TLAXIS/A,B,RA,HI,GAMMA,I,J,K**
C     **TLAXIS/A,B,RA,HI,GAMMA,I,J,K,ALPHA**
C
72    IF(PARAM(1).EQ.Z0.OR.PARAM(1).GE.4.0)GO TO 150
      IF(PARAM(2).GT.K4)GO TO 150
      GO TO 80
   75 IF(PARAM(2).GE.Z3) GO TO 150
   76 PARAM(1)=PARAM(2)+Z1
C...
   80 IF (NUMBER .GE. 9) GOTO 95
      DO 90 I=NUMBER,8
90    PARAM(I+1)=Z0
   95 CONTINUE
100   MANTAX=PARAM(1)
      NUMAX=IP2
      RA=PARAM(3)
      HI=PARAM(4)
      GAMMA=DEGRAD*PARAM(5)
   96 ALP=-DEGRAD*PARAM(9)
      AX4(1)=PARAM(6)
      AX4(2)=PARAM(7)
      AX4(3)=PARAM(8)
      RINGFG=.TRUE.
      IF (SQRT(AX4(1)*AX4(1)+AX4(2)*AX4(2)+AX4(3)*AX4(3)).GT.1.D-10)
     *          CALL VNORM(AX4,AX4)
C
C     **EXIT ROUTES**
C
110   TLHITE(8)=HI
      RI(8)=RA
      SAGL=DSIN(GAMMA)
      CAGL=DCOS(GAMMA)
      IF(MANTAX-K2)140,120,130
120   JTLFLG(IPS)=K1
      GO TO 140
130   JTLFLG(IDS)=K1
140   RETURN
C
C     **ERROR-INVALID TLAXIS FORMAT**
C
  150 IAERFG=1
      CALL AERR(22301,'TLAXIS  ')
      RETURN
C       NO CUTTER DEFINED TO COMPUTE DISC RADIUS RA OR DISC HIGHT HI.
  160 IAERFG=1
      CALL AERR(22219,'TLAXIS  ')
      RETURN
C
C      NEW TLAXIS FORMATS INSTALLED AT 10.12.84
C
C 201 **TLAXIS/ATANGL,PS,GAMMA,RADIUS,RA,HIGHT,HI**
C 212 **TLAXIS/PARLEL,DS,RADIUS,RA,HIGHT,HI**
C
  201 PARAM(5)=180.D0-PARAM(5)
  212 PARAM(1)=Z1
      CALL HOLFRM(PARAM(2),TEST,1,6,NWD)
      IF(TEST.EQ.DS)PARAM(1)=Z2
      PARAM(2)=PARAM(1)
      GOTO 76
C
C 204 **TLAXIS/ATANGL,DS,GAMMA**
C 215 **TLAXIS/PARLEL,PS OR TLAXIS/PARLEL,DS **
C
  204 GAMMA=(180.D0-PARAM(5))*DEGRAD
  215 PARAM(1)=Z1
      CALL HOLFRM(PARAM(2),TEST,1,6,NWD)
      IF(TEST.EQ.DS)PARAM(1)=Z2
      PARAM(2)=PARAM(1)
      GOTO 31
C
C 202 **TLAXIS/ATANGL,DS,GAMMA,RADIUS,RA**
C 213 **TLAXIS/PARLEL,PS,RADIUS,RA**
C
  202 PARAM(5)=180.D0-PARAM(5)
  213 PARAM(4)=Z0
      IF(NCSEG.EQ.0)GOTO 160
      DO 313 I=1,NCSEG
      IF(NLIOCI(I).EQ.1)GOTO 311
      IF(CUTL(I).EQ.0.D0)GOTO 313
C           COMPUTATION OF SEGMENT AND RADIUS ON CIRCLE
      SERA=CUTX(I)+CUTL(I)*SINAL(I)
      IF(SERA.LT.PARAM(3))GOTO 313
C           COMPUTATION OF HI
      PARAM(4)=CUTY(I)-SQRT(CUTL(I)*CUTL(I)-(PARAM(3)-CUTX(I))**2)
      GOTO 212
C           COMPUTATION OF SEGMENT AND RADIUS ON LINE
  311 SERA=CUTX(I)+CUTL(I)*COSAL(I)
      IF(SERA.LT.PARAM(3))GOTO 313
C           COMPUTATION OF HI
      IF(DABS(COSAL(I)).LT.1.D-8)GOTO 312
      PARAM(4)=CUTY(I)+(PARAM(3)-CUTX(I))*SINAL(I)/COSAL(I)
      GOTO 212
  312 PARAM(4)=CUTY(I)+CUTL(I)
      GOTO 212
  313 CONTINUE
      PARAM(4)=CUTY(NCSEG)
      GOTO 212
C
C 203 **TLAXIS/ATANGL,DS,GAMMA,HIGHT,HI**
C 214 **TLAXIS/PARLEL,PS,HIGHT,HI**
C
  203 PARAM(5)=180.D0-PARAM(5)
  214 PARAM(3)=Z0
      IF(NCSEG.EQ.0)GOTO 160
      DO 315 I=1,NCSEG
      IF(NLIOCI(I).EQ.1)GOTO 314
      IF(CUTL(I).EQ.0.D0)GOTO 315
C           COMPUTATION OF SEGMENT AND HEIGHT ON CIRCLE
      SEHI=CUTY(I)-CUTL(I)*COSAL(I)
      IF(SEHI.LT.PARAM(4))GOTO 315
C           COMPUTATION OF RA
      PARAM(3)=SQRT(CUTL(I)*CUTL(I)-(PARAM(4)-CUTY(I))**2)+CUTX(I)
      GOTO 212
C           COMPUTATION OF SEGMENT AND HEIGHT ON LINE
  314 SEHI=CUTY(I)+CUTL(I)*SINAL(I)
      IF(SEHI.LT.PARAM(4))GOTO 315
C           COMPUTATION OF RA
      IF(ABS(SINAL(I)).LT.1.D-8)GOTO 316
      PARAM(3)=CUTX(I)+(PARAM(4)-CUTY(I))*COSAL(I)/SINAL(I)
      GOTO 212
  316 PARAM(3)=CUTX(I)+CUTL(I)
      GOTO 212
  315 CONTINUE
      PARAM(3)=CUTX(NCSEG)
      GOTO 212
C                                     LAG
C 216 **TLAXIS/ATANGL,........,CUTANG,LEAD,ALPHA
C 217 **TLAXIS/ATANGL,........,CUTANG,ALPHA
C
  216 CALL HOLFRM(PARAM(NUMBER-1),TEST,1,6,NWD)
      IF(TEST.EQ.HLAG)PARAM(NUMBER)=-PARAM(NUMBER)
  217 PARAM(9)=PARAM(NUMBER)
      DO 317 I=6,8
  317 PARAM(I)=Z0
      NUMBER=9
      GOTO 331
C                                     LAG
C 218 **TLAXIS/ATANGL,........,CUTANG,LEAD,AUTO
C 219 **TLAXIS/ATANGL,........,CUTANG,AUTO
C
  218 IAUTAG=1
      CALL HOLFRM(PARAM(NUMBER-1),TEST,1,6,NWD)
      IF(TEST.EQ.HLAG)IAUTAG=-1
      GOTO 330
C                                     LINE
C 220 **TLAXIS/ATANGL,........,PARLEL,PLANE
C 221 **TLAXIS/ATANGL,........,PERPTO,VECTOR
C
  220 NUMBER=9
      PARAM(9)=Z0
      IP2=IP2-1
      GOTO 331
C
C 222 **TLAXIS/ATANGL,........,MOTDIR
C
  222 MOTDIR=1
      IP2=IP2-1
C
  330 NUMBER=5
  331 NPRD=IPRD
      GOTO 1
C
      END
