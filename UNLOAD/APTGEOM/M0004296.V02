*
C.....FORTRAN SUBROUTINE             APT016...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT016
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A LINE DEFINED
C              AS TANGENT TO TWO CIRCLES BY THE FOLLOWING APT
C              STATEMENT
C              RESULT = LINE/****, TANTO, CIRC1, ****, TANTO,
C                       CIRC2
C                **** = LEFT OR RIGHT
C
C LINKAGE      CALL APT016 (RESULT, M, CIRC1, N, CIRC2)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM OF
C                      THE RESULTING LINE
C              M       INTEGER EQUIVALENT OF THE FIRST MODIFIER
C                             1 = RIGHT        2 = LEFT
C              CIRC1   ARRAY CONTAINING THE CANONICAL FORM OF
C                      THE FIRST CIRCLE
C              N       INTEGER EQUIVALENT OF THE SECOND MODIFIER
C                             1 = RIGHT       2 = LEFT
C              CIRC2   ARRAY CONTAINING THE CANONICAL FORM OF
C                      THE SECOND CIRCLE
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT077
C              SUBROUTINE          APT083
C              REAL FUNCTION       ABS
C              SUBROUTINE          DEFEXT
C              REAL FUNCTION       SQRT
C
      SUBROUTINE APT016 (RESULT,MN, CIRC1,NM, CIRC2)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION (O-Z)
      DIMENSION RESULT(4),CIRC1(7),CIRC2(7)
C
C
C
      INCLUDE (TOTAL)
      INCLUDE (DEF)
      INCLUDE (DSHAR3)
      INCLUDE (ZNUMBR)
      INCLUDE (IDEF)
      INCLUDE (ISHR16)
      INCLUDE (KNUMBR)
C
      EQUIVALENCE (S1,SC(1)),(S2,SC(2)),(S3,SC(3)),(S4,SC(4)),(S5,SC(5))
C
C...     TRANSFORM CANONICAL FORMS AND CHECK INPUT VALIDITY
C
      CALL APT083 (CIRC1,21)
      DO 10 I=1,7
   10 SC(I+5) = C(I)
      CALL APT083 (CIRC2,22)
C
C...     CALCULATE DISTANCE BETWEEN CENTERS CHECK FOR CONCENTRIC,
C...        INTERSECTING CIRCLES OR CENTERS TOO CLOSE.
C
      S1=DSQRT((C(1)-SC(6))**2+(C(2)-SC(7))**2)
      IF (S1.LE.Z1EM5) IERROR=K23
      IF (IERROR.NE. 0) GO TO 30
      IF(DABS(SC(12)-C(7))-S1.GT.Z1EM5) IERROR = K24
      IF (IERROR.NE. 0) GO TO 30
      IF ((S1 -(SC(12) +C(7))).GE.Z1EM5) GO TO 20
      IF (NM.NE.MN) IERROR = K25
   20 IF (IERROR.NE. 0) GO TO 30
C...     CALCULATE SIN(ALPHA) AND COS(ALPHA)
C
      S2 =   (C(2)-SC(7))/S1
      S3 =   (C(1)-SC(6))/S1
      IF (MN.EQ.NM)SC(12) = -SC(12)
C
C...     CALCULATE SIN(GAMMA) AND COS(GAMMA)
C
      S4 = (C(7) +SC(12)) /S1
      S5=DSQRT(Z1-S4*S4)
      IF (NM.EQ.K1) S5 = -S5
C
C...     SET UP CANONICAL FORM IN P AND TRANSFORM TO BASE COORDINATES
C
      P(1)=S4*S3+S5*S2
      P(2)=S4*S2-S5*S3
      P(3)=Z0
      CALL VNORM (P,P)
      P(4)=P(1)*C(1)+P(2)*C(2)-C(7)
      CALL APT077 (P,KM1)
   30 CALL DEFEXT (RESULT)
       RETURN
       END
