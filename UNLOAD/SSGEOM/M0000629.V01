*
      SUBROUTINE FORCUB(PA,PB,PC,PD,TA,TD,IRR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C------INPUT IS FOUR SPACE POINTS PA,PB,PC,PD.   OUTPUT TA, TD DEFINES
C------A PARAMETRIC CUBIC CURVE WITH END POINTS PA,PD AND END TANGENT
C------VECTORS TA,TD  WHICH APPROXIMATES SMOOTHLY THE POINTS PB AND PC
C------THIS IS A COMPLETELY MODULAR APT SCULPTURED SURFACE ROUTINE
C------ WRITTEN AT IITRI APRIL 1970 --- JKH
      DIMENSION TA(3),TD(3),PA(3),PB(3),PC(3),PD(3),QA(3),QB(3),QC(3),
     1 QD(3),T1(3),T2(3),PAV(3),CN(3),DX(3),DY(3),DIF(3),VNULL(3)
      ONE=1.0
      IDIAG=18000
C------AVERAGE INTERIOR POINTS PB,PC INTO PAV
      DO 10 I=1,3
      DIF(I)=PD(I)-PA(I)
   10 PAV(I)=(PB(I) + PC(I))/2.0
      DIFL=DSQRT(DIF(1)**2+DIF(2)**2+DIF(3)**2)
      IRR=IDIAG+1
      IF(DIFL.LT.1.0D-10) RETURN
C------CONSTRUCT THE AVERAGED NORMAL TO PA,PAV,PD
      CALL NORML3(PA,PAV,PD,CN,IRR)
      IF(IRR.NE.0) CALL NORML3(PA,PB,PD,CN,IRR)
      IF(IRR.NE.0) CALL NORML3(PA,PC,PD,CN,IRR)
      IF(IRR.EQ.0) GO TO 20
C------STRAIGHT LINE CASE
      DO 30 I=1,3
      TA(I)=DIF(I)
   30 TD(I)=DIF(I)
      IRR=0
      RETURN
   20 CONTINUE
C------CONSIDER THE COMMON PLANE THROUGH PA,PB WITH NORMAL CN
C------CONSTRUCT ORTHOGONAL COORDINATE VECTORS DX AND DY IN THIS PLANE
      DO 40 I=1,3
   40 DX(I)=DIF(I)/DIFL
      DO 50 I=1,3
   50 VNULL(I)=0.0
      CALL NORML3(VNULL,CN,DX,DY,IRR)
C------NOW PROJECT ALL POINTS PA,PB,PC,PD ONTO THE AVERAGED PLANE.
C------CALL THE PROJECTIONS BY QA,QB,QC,QD
      QA(1)=0.
      QA(2)=0.
      QA(3)=0.
      QD(1)=DIFL
      QD(2)=0.
      QD(3)=0.
      DO 300 J=1,3
      QB(J)=0.
  300 QC(J)=0.
      DO 310 I=1,3
      QB(1)=QB(1) + (PB(I)-PA(I))*DX(I)
      QB(2)=QB(2) + (PB(I)-PA(I))*DY(I)
      QC(1)=QC(1) + (PC(I)-PA(I))*DX(I)
      QC(2)=QC(2) + (PC(I)-PA(I))*DY(I)
  310 CONTINUE
C------NOW INTERPOLATE TANGENT VECTORS AND RADII AT EACH END OF THE
C------CURVE (USING A PARABOLIC APPROXIMATION)
      CALL PARINT(PA,PB,PC,T1,RA,1,IRR)
C------ADJUST THE SIGN OF THE RADIUS
      TEMP=QB(1)*QC(2)-QB(2)*QC(1)
      RA=DABS(RA)*DSIGN(ONE,TEMP)
C------CONVERT FROM RADIUS TO CURVATURE
      IF(RA.GT.1.0D+8) RA=1.0D-8
      IF(RA.LE.1.0D+8) RA=1.0/RA
      CALL PARINT(PD,PC,PB,T2,RD,1,IRR)
      DO 60 I=1,3
   60 T2(I)=-T2(I)
      TEMP=TEMP-DIFL*(QC(2)-QB(2))
      RD=DABS(RD)*DSIGN(ONE,TEMP)
C------CONVERT FROM RADIUS TO CURVATURE
      IF(RD.LE.1.0D+8) RD=1.0/RD
      IF(RD.GT.1.0D+8) RD=1.0D-8
C------NOW END TANGENTS AND END RADII HAVE BEEN APPROXIMATED IN THE
C------AVERAGED PLANE
C------NOW OBTAIN COMPONENTS OF THE INTERPOLATED TANGENTS T1 AND T2 IN
C------THE AVERAGED PLANE
      GX=T1(1)*DX(1) + T1(2)*DX(2) + T1(3)*DX(3)
      GY=T1(1)*DY(1) + T1(2)*DY(2) + T1(3)*DY(3)
      HX=T2(1)*DX(1) + T2(2)*DX(2) + T2(3)*DX(3)
      HY=T2(1)*DY(1) + T2(2)*DY(2) + T2(3)*DY(3)
      GL=DSQRT(GX*GX + GY*GY)
      HL=DSQRT(HX*HX + HY*HY)
C------MODIFY THE SPACE CURVATURES RA AND RD BY THEIR PROJECTIONS ON THE
C------AVERAGED PLANE
      COSA=CN(1)*T1(1) + CN(2)*T1(2) + CN(3)*T1(3)
      TEMP=DABS(ONE-COSA*COSA)
      SINA=DSQRT(TEMP)
      COSD=CN(1)*T2(1) + CN(2)*T2(2) + CN(3)*T2(3)
      TEMP=DABS(ONE-COSD*COSD)
      SIND=DSQRT(TEMP)
      RA=RA/SINA
      RD=RD/SIND
C------NOW SOLVE IN THE AVERAGED PLANE FOR A PARAMETRIC PLANAR CUBIC
C------WITH END POINTS QA,QD AND END TANGENTS GX,GY AND HX,HY , AND
C------WITH END CURVATURE RA AND RB
C------THE OUTPUT ALPH,BETA GIVES THE SIGNED MULTIPLIERS OF THE INPUT
C------VECTORS GX ,GY AND HX,HY.
      CALL CLOCUB(QA(1),QA(2),QD(1),QD(2),GX,GY,HX,HY,RA,RD,ALPH,BETA,
     1 IRR)
      IF(IRR.NE.0) RETURN
C------PROJECT THE PLANAR SOLUTIONS ONTO THE INTERPOLATED SPACE
C------TANGENTS FOR A FINAL ANSWER
      DO 200 I=1,3
      TA(I)=ALPH*T1(I)/GL
  200 TD(I)=BETA*T2(I)/HL
      IRR=0
      RETURN
      END
