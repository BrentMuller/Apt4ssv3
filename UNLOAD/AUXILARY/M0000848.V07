*
C
C.....   FORTRAN SUBROUTINE                 APT202...    4/1/69  RC
C
      SUBROUTINE  APT202(ADRESS,ISRFLG)
      IMPLICIT  DOUBLE PRECISION (A-H,O-Z)
C
C  PURPOSE         TO STORE THE SURFACE NAMES AND SUBSCRIPTS
C                  INTO COMMON BLOCKS DSNAME AND SRFNAM
C
C  CALLING SEQNCE  CALL APT202(ADRESS,ISRFLG)
C
C  ARGUMENTS       ADRESS    INDEX TO SURFACE DATA
C                  ISRFLG    FLAG POINTER FOR SURFACE --
C                                 1.  PART SURFACE
C                                 2.  DRIVE SURFACE
C                                 3.  CHECK SURFACE 1
C                                 4.  CHECK SURFACE 2
C
C  CALLED BY       APT234,  FROM,  GOTO
C
C  SUBSIDIARIES    UNPACK
C  DIAGNOSTICS     NONE
C
C
      INCLUDE (TOTAL)
      INCLUDE (LDEF)
      INCLUDE (IDEF)
C
C
C        COMMON BLOCKS FOR SURFACE NAME AND SUBSCRIPT STORAGE
C
C     23.   DSNAME BLOCK
      COMMON/DSNAME/DSNAME,NAMSUB
C
C     24.   SRFNAM BLOCK
      COMMON /SRFNAM/ PSNAME,CS1NAM,CS2NAM,IPSSUB,ICS1SB,ICS2SB
C
C...     INFORMATION ASSOCIATED WITH CANONICAL FORMS STORAGE
C
      INCLUDE (BLANKCOM)
      DOUBLE PRECISION X(COMSIZ)
      EQUIVALENCE (COM(1),X(1))
      INTEGER  XCANON,ADDRSS
      EXTERNAL XCANON,ADDRSS
C
      EQUIVALENCE  (CVAL,ISKIP)
C
      DIMENSION  ADRESS(*)
      CHARACTER  DOLLAR,TEMP*4,HNAME*8
      LOGICAL  FIRST
      INTEGER  ITEMP(2),BCDF
      EQUIVALENCE  (TMPNAM,ITEMP(1))
      DATA   DOLLAR,HNAME  / '$','NONAME  ' /
C
C     ------------------------------------------------------------
C
      CALL UNPACK(ADRESS,MODE,ISIZE,ISBSCT)
      IF (ISBSCT .GT. 1) GO TO 200
      K=ISIZE+1
      TMPNAM=ADRESS(K)
      CALL HOLFRM(ITEMP,TEMP,1,4,NWD)
    5 IF (DOLLAR .EQ. TEMP(1:1)) THEN
        ITEMP(1) = BCDF(HNAME(1:4))
        ITEMP(2) = BCDF(HNAME(5:6))
      ENDIF
C
C        BRANCH FOR PROPER SURFACE GIVEN BY ISRFLG
      GO TO (10,20,30,40), ISRFLG
C
   10 PSNAME=TMPNAM
      IPSSUB=ISBSCT
      GO TO 100
C
   20 DSNAME=TMPNAM
      NAMSUB=ISBSCT
      GO TO 100
C
   30 CS1NAM=TMPNAM
      ICS1SB=ISBSCT
      GO TO 100
C
   40 CS2NAM=TMPNAM
      ICS2SB=ISBSCT
C
  100 RETURN
C
C
C
C...     DETERMINE VIRTUAL INDEX OF ADRESS(1) IN X-ARRAY
C
  200 I=(ADDRSS(ADRESS) - ADDRSS(X))/KWRDSZ + ISIZE + 1
      K = I
      IF (CANFLG) CALL INCAN(I)
C
C...     FIND BCD NAME OF THIS SURFACE
C
      DO 400 J=2,ISBSCT
  250 CVAL=X(K)
      IF (ISKIP .LE. 0 .OR. ISKIP .GT. 1024) GO TO 300
      I=I+ISKIP
      K = I
      IF (CANFLG) K = XCANON(I)
      GO TO 250
  300 K = K + ISIZE + 1
      I = I + ISIZE + 1
  400 CONTINUE
      TMPNAM = X(K)
      GO TO 5
      END
