*
C.....FORTRAN SUBROUTINE             CCURV ...              3/1/68   GK
      SUBROUTINE CCURV(ISSTO,SP3,SPL3,SN3)
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
C
      INCLUDE (TOTAL)
      INCLUDE (FXCOR)
      INCLUDE (SV)
      INCLUDE (ISV)
      INCLUDE (IFXCOR)
      INCLUDE (ISHR18)
C
      DIMENSION SP3(3),SPL3(3),SN3(3)
      DATA Z1EM3,Z1EM4,Z1EM9,ZERO,Z100/1.D-3,1.D-5,1.D-9,0.D0,100.D0/
C
C             /-INPUT SURFACE PT -SP3, AND NORMAL SN3, AND LAST POINT SP
C             /-ISSTO IS INDEX TO CURRENT SURFACE(2=PART,3=DRIVE ETC)
C             /-FIND SURFACE CENTER OF CURVATURE AND RADIUS(CC,RC)
C
      IF(IPL(ISSTO).EQ.0) GO TO 1
C             /-PLANAR SURFACE CASE - IPL=1
      ISIGCR(ISSTO)=-1
      GO TO 6
    1 CONTINUE
C             /-NON-PLANAR GENERAL CASE
C             /-CHECK FOR SIGNIFICANCE OF SP-SPL
C             /-N=0 MEANS SP-SPL IS TOO SMALL FOR CALCULATION
      N=1
      DO 2 I=1,3
      TEMP(I)=SP3(I)-SPL3(I)
      IF(DABS(TEMP(I)).GT.Z1EM3) N=0
    2 CONTINUE
      IF(N.LE.0) GO TO 3
C             /-SP-SPL INSIGNIFICANT, USE LAST RC
      ISIGCR(ISSTO)=1
      GO TO 6
    3 CONTINUE
C             /-NORMAL CASE FOR GENERAL SURFACE
      CALL VNORM(TEMP,TEMP(4))
      CALL DOTF(W1,TEMP(4),SN3)
      IF(DABS(W1).GE.Z1EM4) GO TO 4
C             /-SP-SPL ALMOST PERPENDICULAR TO SN . SET RC 45 INCH
   30 RC(ISSTO)=DSIGN((RC1/Z100),RC(ISSTO))
      ISIGCR(ISSTO)=-1
      GO TO 6
    4 CONTINUE
C     CHECK, WHETHER SURFACE IS LOCALLY ALMOST PLANAR
      CALL DOTF (W1,TA,SN3)
      IF (DABS(W1).GE.1.D0-Z1EM4.AND.MANTAX.LT.2) GO TO 30
C             /-GENERAL CASE, CALCULATE R SO THAT (SP-R*SN-SPL)**2=R**2
C             /-CENTER OF CURV. IS ON SIDE OF SN IF RC NEGATIVE.
      CALL DOTF(W1,TEMP,SN3)
      CALL DOTF(RC(ISSTO),TEMP,TEMP)
C             /-R=D**2/(2*(D,SN)) WHERE D=SP-SPL
      RC(ISSTO)=RC(ISSTO)/(W1+W1)
C             /-SET FLAG TO NORMAL CASE
      ISIGCR(ISSTO)=0
C             /-COMMON RETURN POINT FOR ALL CASES
    6 CONTINUE
      DO 7 I=1,3
      CC(I,ISSTO)=SP3(I)-RC(ISSTO)*SN3(I)
      IF(DABS(CC(I,ISSTO)).LT.Z1EM9) CC(I,ISSTO)=ZERO
    7 CONTINUE
      RETURN
      END
