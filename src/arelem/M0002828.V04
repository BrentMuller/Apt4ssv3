*
C
C
      SUBROUTINE DDPARA(U,V)
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
C
C
C
      INCLUDE (TOTAL)
C...  ALL REFERENCES TO TEMP HAVE BEEN CHANGED TO TEMP1, AS
C...  TEMP OCCURS IN FXCOR.INC
      INCLUDE (FXCOR)
      INCLUDE (SV)
      INCLUDE (ZNUMBR)
      INCLUDE (ISV)
      INCLUDE (IFXCOR)
      INCLUDE (KNUMBR)
C
      EQUIVALENCE (TEMP1(11),DIF(1)), (TEMP1(13),DISP(1))
      EQUIVALENCE  (TEMP1(19),UU), (TEMP1(20),VV)
      DIMENSION DERU(3,4),DERV(3,4),DIF(2),P(3),DISP(3)
      DIMENSION US(25),VS(25),SPLL(3),SDIF(3),T1(3),T2(3)
      DIMENSION PDUM(3),PDUM1(3)
      LOGICAL CKDEF
      DIMENSION IFSTT(4) ,TEMP1(25)
      DATA IFSTT /4*0/
C
C  --------------------------------------------------------------~
C
      Z1EM4=0.00003
      IDEBUG = 0
      UU=U
      VV=V
      IS2=IS+2
      IP=IS/80
      IF ( JENT(IS).EQ.0) IFSTT(IP)=0
      IF (JENT(IS).EQ.1 .AND. IFSTT(IP).EQ.0 ) GO TO 4040
      MMODE=2
 4039 MODE=2
      GO TO 4090
 4040 MMODE=1
      IF(CKDEF(U)) CALL AERR(27004,'DDPARA  ')
      IF(CKDEF(V)) CALL AERR(27005,'DDPARA  ')
      IFSTT(IP) = 1
      IF ( ISTRUP.EQ.1 .AND. INDIR(IS).EQ.1) GO TO 4039
C------MODE=1 MEANS AN SNXP COMPUTATION IS BEING REQUESTED
C------ COMPUTE THE CLOSEST POINT ON THE TANGENT PLANE TO THE TOOL POINT
      MODE=1
 4090 CONTINUE
      TEM(1)=SN(1,IS)
      TEM(2)=SN(2,IS)
      TEM(3)=SN(3,IS)
      TEM(4)=SP(1,IS)
      TEM(5)=SP(2,IS)
      TEM(6)=SP(3,IS)
C------THE FOLLOWING LOOP ATTEMPTS BY AN ITERATIVE TECHNIQUE, TO
C------FIND THE SURFACE POINT SP AND SURFACE NORMAL SN WHERE THE
C------TOOL RAY TP,TN INTERSECTS THE SURFACE
      LCT=0
   10 CALL LCLS(UU,VV,SP(1,IS),DERU(1,IP),DERV(1,IP),MMODE)
C------THE ABOVE CALL INITIATES A LINKAGE OF A PROGRAMMER SUPPLIED
C------FORTRAN SUBROUTINE WHICH DEFINES THE CURRENT PARAMETRIC
C------SURFACE.  THE PARAMETER VALUES U,V,W ARE INITIALLY SUPPLIED
C------FROM THE PART PROGRAM TO GIVE THIS ALGORITHM A GOOD STARTUP
C------POINT  THE SURFACE PT AND NORMAL CORRESPONDING TO THESE
C------PARAMETER VALUES ARE RETURNED.  THEREAFTER, THE ROUTINE
C------MODIFIES THE U V PARAMETER VALUES UNTIL THEY GENERATE A GOOD
C------SURFACE POINT
      TEMP1(1) = SP(1,IS) - TP(1,IS)
      TEMP1(2) = SP(2,IS) - TP(2,IS)
      TEMP1(3) = SP(3,IS) - TP(3,IS)
   22 S(IS) = TEMP1(1)*SN(1,IS) + TEMP1(2)*SN(2,IS) + TEMP1(3)*SN(3,IS)
C------IF TOOL POINT AND SURF PT ARE CLOSE THEN MAKE NORMAL EXIT
      DTEMP = DSQRT( TEMP1(1)**2+TEMP1(2)**2+TEMP1(3)**2 )
      INORM = 1
       IF ( DTEMP .LT. Z1EM4 ) GO TO 40
C------ IF IMODE=1 CHECK FOR SUFACE NORMAL ALLIGNMENT WITH TOOL POINT
      ZANG=SN(1,IS)*TEMP1(1)+SN(2,IS)*TEMP1(2)+SN(3,IS)*TEMP1(3)
      XANG = DABS(ZANG)/DTEMP
      IF(MODE.EQ.1.AND.DABS(XANG-1.0).LT.Z1EM4) GO TO 40
      ANG = 1.0 - DABS(S(IS)/DTEMP)
      DEN = TN(1,IS)*SN(1,IS) + TN(2,IS)*SN(2,IS) + TN(3,IS)*SN(3,IS)
C------IF THE TOOL NORMAL AND SURFACE NORMAL ARE NEARLY PERPENDICULAR
C------THEN TAKE AN ABNORMAL EXIT
      INORM = 2
      IF ( MODE .EQ. 2 .AND. DABS(DEN) .LT. Z1EM4 ) GO TO 70
C------THE FIRST TIME THROUGH, COMPUTE THE INTERSECTION OF THE
C------TOOL RAY TP,TN AND THE STARTUP SURFACE PLANE
      IF ( DABS(DEN) .GT. 1.0D-6) S(IS)=S(IS)/DEN
C------IF THE SURFACE POINT IS CLOSE TO THE TOOL RAY, THEN EXIT NORMALLY
      DOT = TEMP1(1)*TN(1,IS) + TEMP1(2)*TN(2,IS) +TEMP1(3)*TN(3,IS)
      DO 6031 LL=1,3
 6031 PDUM(LL) = TEMP1(LL) - DOT*TN(LL,IS)
      DDUM = DSQRT( PDUM(1)**2 + PDUM(2)**2 + PDUM(3)**2)
      INORM = 4
      IF ( MODE .EQ. 2 .AND. DDUM .LT. Z1EM4 ) GO TO 40
      IF ( MODE .EQ. 1 ) GO TO 501
C------P IS THE POINT OF INTERSECTION OF TANGENT PLANE AND TOOL RAY
      P(1) = TP(1,IS) + S(IS)*TN(1,IS)
      P(2) = TP(2,IS) + S(IS)*TN(2,IS)
      P(3) = TP(3,IS) + S(IS)*TN(3,IS)
      GO TO 601
  501 CONTINUE
      P(1) = TP(1,IS) +  ZANG*SN(1,IS)
      P(2) = TP(2,IS) +  ZANG*SN(2,IS)
      P(3) = TP(3,IS) +  ZANG*SN(3,IS)
  601 CONTINUE
      DISP(1) = P(1) - SP(1,IS)
      DISP(2) = P(2) - SP(2,IS)
      DISP(3) = P(3) - SP(3,IS)
C------FIND A CHANGE DU,DV IN U,V SUCH THAT DU*DERU+DV*DERV=DISP
      DO 5014 L=1,6
 5014 TEMP1(L)=0
      DO 5015 L=1,3
      TEMP1(1) = TEMP1(1) + DERU(L,IP)*DERU(L,IP)
      TEMP1(2) = TEMP1(2) + DERU(L,IP)*DERV(L,IP)
      TEMP1(3) = TEMP1(3) + DERV(L,IP)*DERV(L,IP)
      TEMP1(5) = TEMP1(5) + DISP(L)  *DERU(L,IP)
 5015 TEMP1(6) = TEMP1(6) + DISP(L)  *DERV(L,IP)
      DO 5016 L=1,6
      IF ( DABS(TEMP1(L)) .LT. 1.0D-30) TEMP1(L)=0.0
 5016 CONTINUE
      TEMP1(4) = TEMP1(1)*TEMP1(3) - TEMP1(2)**2
      IF ( DABS(TEMP1(4)).LT. 1.0D-30 ) GO TO 40
      DIF(1) = ( TEMP1(3)*TEMP1(5) - TEMP1(2)*TEMP1(6) )/ TEMP1(4)
      DIF(2) = ( TEMP1(1)*TEMP1(6) - TEMP1(2)*TEMP1(5) )/ TEMP1(4)
      TU = UU + DIF(1)
      TV = VV + DIF(2)
      IF(MODE.EQ.3) GO TO 3041
C------SAVE THE CURRENT SURFACE NORMAL SINCE LCLS RESETS SN
      DO 6713 L=1,3
 6713 T1(L)=SN(L,IS)
      CALL LCLS(TU,TV,SPLL,PDUM,PDUM1,MMODE)
C------RESTORE THE OLD SURFACE NORMAL
      DO 6714 L=1,3
 6714 SN(L,IS)=T1(L)
C------AFTER THE FIRST PASS, USE THE LAST SURFACE POINT AND PARA-
C------BOLIC INTERPOLATION TO ESTIMATE BETTER U,V VALUES
 3100 CONTINUE
C------FIRST SETUP THE APPROXIMATING PARABOLA TO THE SURFACE
      DO 3040 L = 1,3
 3040 SDIF(L) = SPLL(L) - SP(L,IS)
      CALL VNORM(SDIF,SDIF)
      IF(VTEM.GT.1.0D-12) GO TO 3042
      IFSTT(IP)=3
      MODE=3
 3041 UU=TU
      VV=TV
      GO TO 10
 3042 CALL CROSS(SDIF,SN(1,IS),T1)
      CALL CROSS( T1, SN(1,IS), T2)
      CALL VNORM( T2, T2)
C------ T2 IS NOW THE PARABOLIC TANGENT VECTOR
      PX = SDIF(1)*T2(1) + SDIF(2)*T2(2) + SDIF(3)*T2(3)
      PY = SDIF(1)*SN(1,IS) + SDIF(2)*SN(2,IS) + SDIF(3)*SN(3,IS)
      PCURV = PY/(PX*PX)
C------ THE PARABOLA IS NOW R(S) = SP + S*T2 + S**2*PCURV*SN
      DO 3050 L=1,3
 3050 T1(L) = PCURV*SN(L,IS)
C------ NOW CALL FOR THE CLOSEST APPROACH BETWEEN THE PARABOLA AND
C------AND THE TOOL RAY
      CALL PARLND( TP(1,IS),TN(1,IS),SP(1,IS),T2,T1,S(IS),PDUM,
     1           SA,PDUM1,DDUM,MODE,IOP(IS),IFAIL )
      IF ( IFAIL .EQ. 1 ) GO TO 70
C------ IF THE TOOL RAY LIES CLOSE TO THE PARABOLA, THEN EXIT NORMALLY
C------ON RETURN, S(IS) IS THE DISTANCE ALONG THE TOOL NORMAL FROM
C------TP TO CLOSEST APPROACH TO THE PARABOLA.  SA IS THE PARAMETER
C------VALUE WHICH DEFINES THE POINT ON THE PARABOLA.  OTHER
C------VARIABLES RETURNED (PDUM,PDUM1,DDUM) ARE NOT NEEDED HERE
C------ FROM THIS CONSTRUCT AN ESTIMATED DISPLACEMENT VECTOR
      DO 3060 L = 1,3
      DISP(L) = SA*T2(L)
      IF ( DABS(DISP(L)) .LT. 1.0D-30 ) DISP(L)=0.0
 3060 CONTINUE
 4010 CONTINUE
C------IF DISP IS VERY SMALL, MAKE A NORMAL EXIT
      TEMP1(1) = DISP(1)**2 + DISP(2)**2 + DISP(3)**2
      TEMP1(1) = DSQRT(TEMP1(1))
      INORM = 5
      IF(TEMP1(1).LT.1.0D-06) GO TO 40
C------TEST FOR LOOP COUNTER EXCEEDED
      IF( LCT .GT. 24 ) GO TO 70
C------FIND A CHANGE DU,DV IN U,V SUCH THAT DU*DERU+DV*DERV=DISP
   85 DO 3070 L=1,6
 3070 TEMP1(L) = 0.
      DO 3080 L = 1,3
      TEMP1(1) = TEMP1(1) + DERU(L,IP)*DERU(L,IP)
      TEMP1(2) = TEMP1(2) + DERU(L,IP)*DERV(L,IP)
      TEMP1(3) = TEMP1(3) + DERV(L,IP)*DERV(L,IP)
      TEMP1(5) = TEMP1(5) + DISP(L)  *DERU(L,IP)
 3080 TEMP1(6) = TEMP1(6) + DISP(L)  *DERV(L,IP)
      DO 3085 L=1,6
      IF ( DABS(TEMP1(L)) .LT. 1.0D-30) TEMP1(L)=0.0
 3085 CONTINUE
      TEMP1(4) = TEMP1(1)*TEMP1(3) - TEMP1(2)**2
      IF ( DABS(TEMP1(4)).LT. 1.0D-30 ) GO TO 40
      DIF(1) = ( TEMP1(3)*TEMP1(5) - TEMP1(2)*TEMP1(6) )/ TEMP1(4)
      DIF(2) = ( TEMP1(1)*TEMP1(6) - TEMP1(2)*TEMP1(5) )/ TEMP1(4)
      UU = UU + DIF(1)
      VV = VV + DIF(2)
      LCT = LCT + 1
      US(LCT) = UU
      VS(LCT) = VV
C------INTRODUCE ARITHMETIC MEANS TO AVOID OSCILLATIONS
      IF ( LCT .LT. 9 ) GO TO 250
      UU = 0.
      VV = 0.
      DO 260 LL = 9, LCT
      UU = UU + US(LL)
  260 VV = VV + VS(LL)
      UU = UU/(LCT-8)
      VV = VV/(LCT-8)
  250 CONTINUE
      GO TO 10
C------        END OF U,V ITERATIVE LOOP LOGIC
   40 IF(MODE.NE.1) GO TO 4050
      IF ( DTEMP .LT. Z1EM4 ) GO TO 4050
      TN(1,IS)=SP(1,IS)-TP(1,IS)
      TN(2,IS)=SP(2,IS)-TP(2,IS)
      TN(3,IS)=SP(3,IS)-TP(3,IS)
      S(IS)=DSQRT(TN(1,IS)**2+TN(2,IS)**2+TN(3,IS)**2)
      CALL VNORM (TN(1,IS),TN(1,IS) )
 4050 CONTINUE
   55 SNL(1,IS) = TEM(1)
      SNL(2,IS) = TEM(2)
      SNL(3,IS) = TEM(3)
      SPL(1,IS) = TEM(4)
      SPL(2,IS) = TEM(5)
      SPL(3,IS) = TEM(6)
C------IF STARTUP IS FINISHED , LET U,V ASSUME THE LAST DEFINED
C------UU,VV VALUES
      U=UU
      V=VV
      IF(IOP(IS).NE.1.OR.S(IS).GE.0.0) GO TO 999
C------  ERROR EXIT FOLLOWS
   70 CONTINUE
C------ IF TOOL NORMAL DIDNOT INTERSECT SURFACE, TRY TO FIND AN SRP
      IF ( MODE .EQ. 2 ) GO TO 1740
      IER = 1
  999 CONTINUE
      JENTL=JENT(IS)
      RETURN
 1740 CONTINUE
      MODE = 1
      UU = U
      VV = V
      GO TO 4090
      END
