*
C
C.... FORTRAN SUBROUTINE              APT041               4/17/68
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A VECTOR DEFINED
C              AS PARALLEL TO THE INTERSECTION OF TWO GIVEN PLANES
C              BY THE FOLLOWING APT STATEMENT
C              VEC = VECTOR/PARLEL,INTOF,PL1,PL2,***
C              *** = POSX,POSY,POSZ,NEGX,NEGY,NEGZ
C
C LINKAGE      CALL APT041 (RESULT,PL1,PL2,MN)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM
C                      OF THE RESULTING VECTOR
C              PL1     ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE FIRST INPUT PLANE
C              PL2     ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE SECOND INPUT PLANE
C              MN      INTEGER EQUIVALENT OF THE MODIFIER
C                      1 = POSX  2 = POSY  3 = POSZ
C                      4 = NEGX  5 = NEGY  6 = NEGZ
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT077
C              SUBROUTINE          APT078
C              SUBROUTINE          CROSS
C              SUBROUTINE          DEFEXT
C              SUBROUTINE          VNORM
C              REMAINDERING FUNCTION  MOD
C              REAL FUNCTION       SIGN
C              REAL FUNCTION       FLOAT
C
      SUBROUTINE APT041 (RESULT,PL1,PL2,MN)
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
      DIMENSION RESULT(3), PL1(4), PL2(4)
C
C
C
      INCLUDE (TOTAL)
      INCLUDE (DEF)
      INCLUDE (DSHAR3)
      INCLUDE (ZNUMBR)
      INCLUDE (ISHR16)
      INCLUDE (KNUMBR)
C
C
C
C...     TRANSFORM CANONICAL FORMS AND CHECK VALIDITY
C
      CALL APT077 (PL1,K0)
      DO 10 J = 1,4
   10 SC(J) = L(J)
      CALL APT077 (PL2,K0)
C
C...     DETERMINE VECTOR PARALLEL TO INTERSECTION OF THE TWO PLANES
C
      CALL CROSS (SC,L,L)
      CALL VNORM (L,L)
C
C...     IF VECTOR HAS WRONG ORIENTATION, REVERSE DIRECTION
C
      I = K1+MOD(MN-K1,K3)
      DUMMY1 = MN
      SC(4) = DSIGN(Z1,L(I)*(3.5D0-DUMMY1))
      DO 20 J = 1,3
   20 L(J) = SC(4)*L(J)
C
      CALL APT078 (L,KM1)
      CALL DEFEXT (RESULT)
      RETURN
       END
