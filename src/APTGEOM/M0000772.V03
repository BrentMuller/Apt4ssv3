*
C
C.....FORTRAN SUBROUTINE              APT022                3/5/68
C
C              FORTRAN SUBROUTINE APT022
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A LINE DEFINED
C              AS PASSING THROUGH A GIVEN POINT AND FORMING A
C              SPECIFIED ANGLE OR SLOPE WITH A GIVEN LINE BY THE
C              FOLLOWING APT STATEMENTS
C              RESULT = LINE/PN, ATANGL, PSI, LIN
C              RESULT = LINE/PN, SLOPE, PSI, LIN
C
C LINKAGE      CALL APT022 (RESULT,PN,MN,PSI,LIN)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM OF THE
C                      RESULTING LINE
C              PN      ARRAY CONTAINING THE CANONICAL FORM OF THE
C                      INPUT POINT
C              MN      INTEGER EQUIVALENT OF THE MODIFIER
C                      1 = ATANGL  2 = SLOPE
C              PSI     REAL VARIABLE CONTAINING THE VALUE OF
C                      THE ANGLE (IN DEGREES) OR SLOPE
C              LIN     ARRAY CONTAINING THE CANONICAL FORM OF THE
C                      INPUT LINE
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT077
C              SUBROUTINE          APT081
C              SUBROUTINE          DEFEXT
C              SUBROUTINE          VNORM
C              REAL FUNCTION       SIN
C              REAL FUNCTION       COS
C              REAL FUNCTION       ATAN
C
      SUBROUTINE APT022 (RESULT,PN,MN,PSI,LIN)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION(O-Z)
      DIMENSION RESULT(4),PN(3),LIN(4)
      DOUBLE PRECISION LIN
C
C
C
      INCLUDE (TOTAL)
      INCLUDE (DEF)
      INCLUDE (DSHAR3)
      INCLUDE (ZNUMBR)
      INCLUDE (ISHR16)
      INCLUDE (KNUMBR)
C
      EQUIVALENCE (S1,SC(1)),(S2,SC(2)),(S3,SC(3))
C
C...     TRANSFORM PN, LIN, CHECK VALIDITY
C
      CALL APT081(PN,0)
      CALL APT077(LIN,K26)
      IF (MN.EQ.K1) GO TO 10
      S1 = DATAN(PSI)
      GO TO 20
   10 S1 = PSI*DEGRAD
   20 S2 = DSIN(S1)
      S3 = DCOS(S1)
C
C...     CALCULATE UNIT NORMAL R(1),R(2)
C
      R(1)=L(1)* S3 -L(2)*S2
      R(2)=L(1)* S2 +L(2)*S3
      R(3)=0.0
      CALL VNORM(R,R)
C
      R(4)=P(1)*R(1)+P(2)*R(2)
      CALL APT077(R,KM1)
      CALL DEFEXT (RESULT)
      RETURN
       END
