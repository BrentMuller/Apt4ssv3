**** SOURCE FILE : M0006628.V04   ***
*
C              FORTRAN SUBROUTINE COMSTR
C
C LINKAGE      SUBROUTINE COMSTR
C
C          SUBSIDIARIES                  CALLED BY
C          TYPE          ENTRY           TYPE          ENTRY
C          SUBROUTINE    ASNADZ          SUBROUTINE    MACREC
C          SUBROUTINE    ASNJPZ
C          LOGICAL FCT.  BITMCH
C          SUBROUTINE    CALLZZ
C          SUBROUTINE    JUMPZZ
C          SUBROUTINE    ALARM
C          SUBROUTINE    LABELZ
C          SUBROUTINE    MACLBL
C          INTEGER FCT.  NAME
C          SUBROUTINE    RETMCX
C          SUBROUTINE    RLSLIN
C
      SUBROUTINE COMSTR
C
      IMPLICIT INTEGER (A-Z)
C
C              FUNCTION DECLARATION
C
      LOGICAL BITMCH
C
C        1.    NAME TABLE AND TABLE LIMITS
C
      INCLUDE 'NAMETB.INC'
C NAMTBL:      INTEGER CORRESPONDING TO NAME TABLE
C CNAMTB:      NAME TABLE OR DICTIONARY
C
C        3.    METALINGUISTIC VARIABLE CLASS CODES
C
      COMMON/CODE/CODE
      DIMENSION CODE(150),LABEL(2)
C LABEL:       (1)=CODE CLASS 22000,(2)=NAMTBL INDEX OF 'LABEL '
      EQUIVALENCE (LABEL(1),CODE(43))
C
C       22.    MACRO PROCESSING VARIABLES
C
      COMMON/MACXX1/MACXX1
      DIMENSION MACXX1(71)
C MCSAVP:      PRESENT NEST LEVEL DURING MACRO PROCESSING
      EQUIVALENCE (MCSAVP,MACXX1(5))
C MAXSPS:      NUMBER OF MACRO VARIABLES+1 FOR CURRENT MACRO
      EQUIVALENCE (MAXSPS,MACXX1(21))
C NUMCAL:      CONTAINS NUMBER OF CALLS FOR CURRENT MACRO
      EQUIVALENCE (NUMCAL,MACXX1(28))
C BITWDS:      NUMBER OF WORDS NEEDED FOR ALL CALL QUALITIES OF MACRO
      EQUIVALENCE (BITWDS,MACXX1(29))
C MACTOP:      POINTER TO EXPRESSION NUMBER IN MACSPD TABLE
      EQUIVALENCE (MACTOP,MACXX1(42))
C NUMBIT:      CONTAINS NUMBER OF BITS IN A FIXED POINT WORD
      EQUIVALENCE (NUMBIT,MACXX1(56))
C MCSPDP:      POINTER TO LAST ENTRY IN MACSPD TABLE
      EQUIVALENCE (MCSPDP,MACXX1(57))
C MCSRCH:      AT FINAL MACRO PRECESSING MACDF1 POINTER
      EQUIVALENCE (MCSRCH,MACXX1(59))
C NUMARG:      NUMBER OF MACRO VARIABLES+1 FOR CURRENT MACRO
      EQUIVALENCE (NUMARG,MACXX1(61))
C
C       24.    MACRO PROCESSING ARRAYS
C
      COMMON/MACXX3/MACXX3
      DIMENSION MACXX3(1430),MACDF1(400)
C MACDF1:      STORES MACRO NAME AND ALL MACRO VBL NAMTBL POINTERS
      EQUIVALENCE (MACDF1(1),MACXX3(1))
C
C      25A.    TEMPORARY STORAGE DURING FINAL MACRO PROCESSING
C
      COMMON/MACXX5/ITMPSV
      DIMENSION ITMPSV(135)
C
C       26.    NAMTBL POINTERS OF THE ASSIGNED VALUES FOR MACRO CALL
C
      COMMON/MACXX6/MACSPD
      DIMENSION MACSPD(1300)
C*
C PURPOSE      FOR EACH CALL AND NORMAL VALUE INITIALIZATION
C              TO GENERATE AND OUTPUT I.L. COMMANDS THAT
C              MOVE ASSIGNED VALUE AND BRANCHING LABEL
C              ADDRESSES INTO THE APPROPRIATE MACRO
C              VARIABLE AND LINKAGE LOCATIONS.
C
C NOTES        1.  COMSTR IS ACCESSED AFTER ALL BRANCHING
C                  AND COMMON I.L. TEXT HAS BEEN GENERATED
C                  FOR A MACRO.
C              2.  FOR EACH ACTUAL BRANCHING SITUATION THE
C                  MACSPD TABLE CONTAINS THE FOLLOWING
C                  INFORMATION STREAM.
C                   M = NUMBER OF WORDS NEEDED FOR CALL
C                       QUALITIES.
C                   N = NUMBER OF DIFFERENT BRANCHES.
C                  1)         NEGATIVE OF BRANCHING EXPRESSION
C                             NUMBER.
C                  2          CALL QUALITY WORDS.  QUALITIES
C                  THRU       SET FOR CALLS ASSOCIATED WITH
C                  M+1)       THE FOLLOWING BRANCH LABEL.
C                  M+2)       1ST BRANCH LABEL.  NEGATIVE
C                             IF ONLY NORMAL VALUES USED.
C                  (N-1) (M+2)+2 CALL QUALITY WORDS.
C                  THRU N(M+1))
C                  N(M+1)+1)    NTH BRANCH LABEL.  NEGATIVE IF
C                               ONLY NORMAL VALUES USED.
C
      external alarm
      CHARACTER*6 I297
      DATA I297,J297/'APT297',19206/
C**
      I=NAME(I297)
      NAMTBL(I)=J297
      CALL CALLZZ(I)
      CALL RLSLIN
C
C...     GENERATE RETURN TERMINATOR FOR MACRO COMMON TEXT.
C
      CALL RETMCX(1)
      IF (NUMARG.EQ.0) RETURN
C
C...     INITIALIZE NORMAL VALUE STORAGE.
C
      DO 80 I=1,MAXSPS
   80 ITMPSV(I)=0
C
C...     ARGCNT=NUMBER OF MACRO VARIABLES FOR CORRENT MACRO.
C
      ARGCNT=NUMARG-1
C
C...     ONE ITERATION PER CALL.
C
      DO 200 I=1,NUMCAL
C
C...     COMPUTE WORD POSITION (WDCAL) AND QUALITY (BITCAL) ASSOCIATED
C...     WITH THIS CALL.
C
      WDCAL=(I-1)/NUMBIT
      BITCAL=I-WDCAL*NUMBIT
C
C...     CALCULATE LABEL  FOR CALL INITIALIZATION. LABEL NAME BASED ON
C...     MACRO NAMTBL POINTER AND CALL NUMBER.
C
      CALL MACLBL(MACDF1(MCSRCH),-I,J)
      NAMTBL(J) = LABEL(1)
C
C...     GENERATE LABEL COMMAND.
C
   20 CALL LABELZ(J)
C
C...     ONE ITERATION PER MACRO VARIABLE.
C
   30 DO 1 J=1,ARGCNT
      NUMARG=NUMARG+1
C
C...     IF NEGATIVE, ASSIGNED VALUE=NORMAL VALUE.
C
      IF(MACSPD(NUMARG))40,1,41
C
C...     SAVE IN VBLSAV(J) USED NORMAL VALUE ASSIGNED TO JTH MACRO VAR..
C
   40 ITMPSV(J)=-MACSPD(NUMARG)
      GO TO 1
C
C...     I.L. TO PLACE ADDRESS OF ASSIGNED VALUE IN MACRO VARIABLE WORD.
C
   41 CALL ASNADZ(MACSPD(J),MACSPD(NUMARG))
    1 CONTINUE
C
C...     BEGIN SEARCH IN THE APPROPRIATE SECTION OF THE MACSPD TABLE
C...     (MACCNT) FOR BRANCHING LABELS ASSOCIATED WITH THIS CALL.
C
      MACCNT=MCSPDP
C
C...     ARGSCR KEEPS TRACK OF CURRENT BRANCHING SITUATION.
C
      ARGSCR=ARGCNT+2
    2 MACCNT=MACCNT+1
C
C...     BRANCH IF ALL LABELS HAVE BEEN ASSIGNED FOR THIS CALL.
C
      IF(ARGSCR.GT.MAXSPS)GO TO 11
   50 K=MACCNT+WDCAL
      MACCNT = MACCNT+BITWDS+1
C
C...     FOR CURRENT BRANCHING SITUATION IF BRANCHING LABEL FOR CURRENT
C...     CALL IS FOUND BITMCH=.TRUE.. IF LOOP IS EXHAUSTED WITHOUT
C...     LOCATING LABEL ISSUE DIAGNOSTIC.
C
      IF(BITMCH(MACSPD(K+1),BITCAL))GO TO 10
      IF((MACCNT.LT.MACTOP).AND.(MACSPD(MACCNT+1).GE.0))GO TO 50
C
C...     1021 - ERROR IN FINAL MACRO PROCESSING
C
      CALL ALARM(1021,0,8,'COMSTR  ')
      GO TO 11
C
C...     IF CALL BRANCHING LABEL DOES NOT INVOLVE ONLY NORMAL VALUES
C...     THEN BRANCH.
C
   10 IF(MACSPD(MACCNT).GT.0)GO TO 6
C
C...     SAVE BRANCHING LABEL FOR BRANCH INVOLVING ONLY NORMAL VALUES IN
C...     POSITION OF VBLSAV ARRAY CORRESPONDING TO CURRENT BRANCHING
C...     SITUATION.
C
      ITMPSV(ARGSCR)=-MACSPD(MACCNT)
      GO TO 7
C
C...     CALCULATE LABEL NAME BASED ON MACRO NEST LEVEL AND NUMBER OF
C...     CURRENT BRANCHING SITUATION.
C
    6 CALL MACLBL(-MCSAVP,-ARGSCR,J)
      NAMTBL(J)=LABEL(1)
      CALL ASNJPZ(J,MACSPD(MACCNT))
C
C...     I.L. TO PLACE CALL BRANCHING LABEL IN APPROPRIATE LINKAGE CELL
C...     SO THAT CORRECT BRANCH FOR THIS BRANCHING SITUATION IS
C...     SELECTED WHEN EXECUTING MACRO FOR THIS CALL.
C
    7 ARGSCR=ARGSCR+1
    8 IF(MACCNT.GE.MACTOP)GO TO 11
      IF(MACSPD(MACCNT+1).LT.0)GO TO 2
C
C...     LOCATE NEXT BRANCHING SITUATION INFORMATION.
C
      MACCNT=MACCNT+BITWDS+1
      GO TO 8
C
C...     CALCULATE LABEL NAME OF MACRO COMMON TEXT AND GENERATE A
C...     TRANSFER TO IT AS A CALL INITIALIZATION TERMINATOR.
C
   11 CALL MACLBL(-1,MACDF1(MCSRCH),J)
      NAMTBL(J)=LABEL(1)
      CALL JUMPZZ(J)
  200 NUMARG=NUMARG+1
C
C...     GENERATE LABEL FOR NORMAL VALUE INITIALIZATION.
C
      CALL LABELZ(MACDF1(MCSRCH))
C
C...     FOR EACH MACRO VARIABLE AND EACH BRANCHING SITUATION USING
C...     NORMAL VALUES (VBLSAV(J).GT.0) ASSIGN NORMAL VALUE AND LABEL
C...     ADDRESSES TO THE CORRESPONDING MACRO VARIABLE AND LINKAGE CELLS
C
      DO 100 I=1,MAXSPS
      IF(ITMPSV(I).EQ.0)GO TO 100
      IF(I-ARGCNT)110,110,120
  110 CALL ASNADZ(MACSPD(I),ITMPSV(I))
      GO TO 100
  120 CALL MACLBL(-MCSAVP,-I,J)
      NAMTBL(J)=1
      CALL ASNJPZ(J,ITMPSV(I))
  100 CONTINUE
C
C...     GENERATE RETURN TERMINATOR FOR NORMAL VALUE INITIALIZATION.
C
      CALL RETMCX(2)
      RETURN
       END
