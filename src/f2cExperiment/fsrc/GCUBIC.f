**** SOURCE FILE : M0000630.V01   ***
*
C
      SUBROUTINE GCUBIC(P,Q,R,S,Z,NZ)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C------ MODULAR SCULPTURED SURFACE ROUTINE ALSO SHARED BY PARAMETRIC
C------ SURFACES. GENERAL SOLUTION OF CUBIC EQN.
C------ INPUT ARE COEFFICIENTS OF CUBIC EQN' P*X**3+Q*X**2+R*X+S=0.
C------ OUTPUT IS SET OF REAL ROOTS Z(I), WHERE I=1 TO NZ
C------ NZ IS THE TOTAL NUMBER OF REAL ROOTS.
C------ PROGRAM HANDLES QUADRATICS AND LINEAR EQNS AS SPECIAL CASES
      DIMENSION Z(3)
      DATA ONE,THD,SMAL/1.D0,0.333333333333333D0,1.0D-10/
      DATA PI,HPI,TPI,TPIO3/3.1415926536D0,1.5707963268D0,
     X                    6.2831853072D0,2.0943951024D0/
      NZ=0
      Z(1)=9999.0D0
      Z(2)=9999.0D0
      Z(3)=9999.0D0
      IF(DABS(P).LT.SMAL) GO TO 200
      TR=Q/(3.0*P)
      A=(R-Q*TR)/P
      B=((2*THD*Q*TR-R)*TR+S)/P
      D=B*B/4.0+A*A*A/27.0
      BH=-B/2.0
      IF(D) 20,10,10
   10 DS=DSQRT(D)
      T1=BH+DS
      T2=BH-DS
      S1=DSIGN(ONE,T1)
      S2=DSIGN(ONE,T2)
      E=S1*(S1*T1)**THD+S2*(S2*T2)**THD
      F=A+E*E
      D=E*E-4.0*F
      NZ=1
      Z(1)=E-TR
      IF(D) 999,30,30
   30 D=DSQRT(D)
      Z(2)=(-E+D)/2.0-TR
      Z(3)=(-E-D)/2.0-TR
      NZ=3
  999 RETURN
   20 S2=DSQRT(-D)
      S1=BH
      T1=DSQRT(S1*S1+S2*S2)
      T2=DARCOS(S1/T1)
      IF(S2.LT.0.0) T2=T2+PI
      BH=2.0*T1**THD
      W1=T2/3.0
      Z(1)=BH*DCOS(W1)-TR
      Z(2)=BH*DCOS(W1-TPIO3)-TR
      Z(3)=BH*DCOS(W1-TPIO3-TPIO3)-TR
      NZ=3
      RETURN
  200 IF(DABS(Q).LT.SMAL) GO TO 300
      D=R*R-4.0*Q*S
      IF(D.LT.0.0) RETURN
      D=DSQRT(D)
      W1=Q+Q
      Z(1)=(-R+D)/W1
      Z(2)=(-R-D)/W1
      NZ=2
      RETURN
  300 IF(DABS(R).LT.SMAL) RETURN
      Z(1)=-S/R
      NZ=1
      RETURN
      END
