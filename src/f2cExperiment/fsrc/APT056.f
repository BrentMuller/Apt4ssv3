**** SOURCE FILE : M0000806.V02   ***
*
C.....FORTRAN SUBROUTINE             APT056...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT056
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A CONE
C              DEFINED AS HAVING A GIVEN VERTEX POINT, A
C              GIVEN AXIS VECTOR, AND A GIVEN VALUE OF ITS
C              HALF ANGLE BY THE FOLLOWING APT STATEMENT
C              RESULT = CONE/PN, VE, BETA
C
C LINKAGE      CALL APT056 (RESULT, PN, VE, BETA)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM
C                      OF THE RESULTING CONE
C              PN      ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE INPUT POINT
C              VE      ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE INPUT VECTOR
C              BETA    REAL VARIABLE CONTAINING THE VALUE
C                      OF THE DESIRED HALF ANGLE (DEGREES)
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT040
C              LOGICAL FUNCTION    CKDEF
C              REAL FUNCTION       COS
C              SUBROUTINE          DEFEXT
C              SUBROUTINE          ERROR
C              LOGICAL FUNCTION    ZVECT
C
      SUBROUTINE APT056 (RESULT,PN,VE,BETA)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION (O-Z)
      DIMENSION RESULT(7),PN(3),VE(3)
      LOGICAL CKDEF,ZVECT
C
C
C
      INCLUDE 'TOTAL.INC'
      INCLUDE 'DEF.INC'
      INCLUDE 'DSHAR3.INC'
      INCLUDE 'ZNUMBR.INC'
      INCLUDE 'LDEF.INC'
      INCLUDE 'IDEF.INC'
      INCLUDE 'ISHR16.INC'
      INCLUDE 'KNUMBR.INC'
C
C
C
C...     CHECK INPUT VALIDITY, STORE ARGUMENTS
C
      DO 10 I=1,3
      R(I)=PN(I)
   10 R(I+3)=VE(I)
      UNFLAG=CKDEF(R)
C
C...     CHECK HALF-ANGLE .LE. 90 DEGREES, VECTOR NON-ZERO
C
      IF (BETA.GT.Z90 ) IERROR=K48
      IF (.NOT.ZVECT(R(4))) IERROR=K47
C
C...     NORMALIZE VECTOR, TAKE COSINE OF BETA
C
      CALL APT040(R(4),R(4))
      R(7)=DCOS(BETA*DEGRAD)
      CALL DEFEXT(RESULT)
      RETURN
       END
