**** SOURCE FILE : M0000792.V03   ***
*
C.....FORTRAN SUBROUTINE ....APT042              8/68                SR
C.... FORTRAN SUBROUTINE              APT042               5/6/68
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A VECTOR WHICH
C              MAKES A GIVEN ANGLE WITH A GIVEN LINE DEFINED BY
C              THE FOLLOWING APT STATEMENT
C              VEC = VECTOR/ATANGL,VAL,LN,***
C              *** = POSX,POSY,NEGX,NEGY
C
C LINKAGE      CALL APT042(RESULT,VAL,LN,MN)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM
C                      OF THE RESULTING VECTOR
C              VAL     MEASURE (IN DEGREES) OF THE GIVEN ANGLE
C              LN      ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE GIVEN LINE
C              MN      INTEGER EQUIVALENT OF THE MODIFIER
C                      1 = POSX  2 = POSY  4 = NEGX  5 = NEGY
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT077
C              SUBROUTINE          APT078
C              SUBROUTINE          DEFEXT
C              REAL FUNCTION       DSIGN
C              REAL FUNCTION       DCOS
C
      SUBROUTINE APT042(RESULT,VAL,LN,MN)
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
      DIMENSION RESULT(3),LN(4)
      DOUBLE PRECISION LN
C
C
      INCLUDE 'TOTAL.INC'
      INCLUDE 'DEF.INC'
      INCLUDE 'DSHAR3.INC'
      INCLUDE 'ZNUMBR.INC'
      INCLUDE 'ISHR16.INC'
      INCLUDE 'KNUMBR.INC'
C
C
C...     TRANSFORM CANONICAL FORM AND CHECK VALIDITY
C
      CALL APT077(LN,K0)
C
      VA=VAL*DEGRAD
      SINA=DSIN(VA)
      COSA=DCOS(VA)
C
C...     DETERMINE REQUIRED VECTOR
      V(1)=L(1)*SINA+L(2)*COSA
      V(2)=L(2)*SINA-L(1)*COSA
C
C...     IF VECTOR HAS WRONG ORIENTATION, REVERSE DIRECTION
C
      IF(((MN.EQ.K1).AND.(V(1).GE.Z0)).OR.((MN.EQ.K2).AND.(V(2).GE.Z0)).
     1OR.((MN.EQ.K4).AND.(V(1).LT.Z0)).OR.((MN.EQ.K5).AND.(V(2).LT.Z0)))
     2GO TO 10
      V(1)=-V(1)
      V(2)=-V(2)
   10 V(3)=Z0
C
      CALL APT078(V,KM1)
      CALL DEFEXT(RESULT)
      RETURN
       END
