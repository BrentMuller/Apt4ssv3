**** SOURCE FILE : M0000647.V01   ***
*
C
      SUBROUTINE PARLND(P,R,A,B,C,T,S,MODE)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C-- MODULAR   PURPOSE IS TO COMPUTE THE CLOSEST APPROACH BETWEEN A
C--  PARABOLA IN SPACE AND A SPACE POINT, LINE OR HALF RAY.
C--  INPUT' POINT P AND TOOL RAY R( BOTH VECTORS)
C--          VECTOR COEFFICIENTS OF PARABOLA A,B AND C (EQUATION IS
C--            ( A+ B*S +C*S**2 )
C--          CONTROL VARIABLE  IS MODE.  =-1 MEANS SHORTEST
C--          DISTANCE FROM FULL LINE,  =0 MEANS SHORTEST DISTANCE
C--          FROM POINT AND =1 MEANS SHORTEST DISTANCE FROM HALF LINE.
C--
C--  OUTPUT'  NUMBERS T(FOR LINE) AND S(FOR PARABOLA) WHICH GENERATE THE
C--           SOLUTION POINTS ON THE TOOL RAY(P+T*R) AND CURVE(A+
C--           B*S+C*S**2) RESPECTIVELY.
      DIMENSION P(3),R(3),A(3),B(3),C(3),Z(3)
      D1=A(1)-P(1)
      D2=A(2)-P(2)
      D3=A(3)-P(3)
      DB=D1*B(1)+D2*B(2)+D3*B(3)
      BSQ=B(1)*B(1)+B(2)*B(2)+B(3)*B(3)
      DC=D1*C(1)+D2*C(2)+D3*C(3)
      BC=B(1)*C(1)+B(2)*C(2)+B(3)*C(3)
      CSQ=C(1)*C(1)+C(2)*C(2)+C(3)*C(3)
      DSQ=D1*D1+D2*D2+D3*D3
      X1=DB
      X2=BSQ+DC+DC
      X3=3.0*BC
      X4=CSQ+CSQ
      IF(MODE) 20,30,20
   20 RSQ=R(1)*R(1)+R(2)*R(2)+R(3)*R(3)
      BR=B(1)*R(1)+B(2)*R(2)+B(3)*R(3)
      CR=C(1)*R(1)+C(2)*R(2)+C(3)*R(3)
      DR=D1*R(1)+D2*R(2)+D3*R(3)
      X1=X1-DR*BR/RSQ
      X2=X2-(BR*BR+2.0*DR*CR)/RSQ
      X3=X3-3.0*BR*CR/RSQ
      X4=X4-2.0*CR*CR/RSQ
   30 CONTINUE
      CALL GCUBIC(X4,X3,X2,X1,Z,NZ)
      IF(NZ.GT.1) GO TO 40
      S=Z(1)
      T=0
      IF(MODE.EQ.0) RETURN
      T=(DR+S*(BR+S*CR))/RSQ
      RETURN
   40 IF(MODE) 100,200,100
  100 DST=99999999.0
      DO 110 I=1,NZ
      SCUR=Z(I)
      TCUR=(DR+SCUR*(BR+SCUR*CR))/RSQ
      IF(TCUR.LT.0.0.AND.MODE.EQ.1) GO TO 110
C-----X1=DSQ+SCUR*(DB+DB+SCUR*(DC+DC+BSQ+SCUR*(BC+BC+SCUR*CSQ)))
C-----X2=DR+SCUR*(BR+SCUR*CR)
C-----TEMP=X1-TCUR*(X2+X2-TCUR*RSQ)
      TEMP=1.0-SCUR
      TEMP=DABS(TEMP)
C------  DISRCIMINATE AGAINST A NEGATIVE SCUR
      IF(SCUR.LT.0) TEMP=10*TEMP
      IF(TEMP.GT.DST) GO TO 110
      DST=TEMP
      S=SCUR
      T=TCUR
  110 CONTINUE
      RETURN
  200 DST=9999999.0
      DO 210 I=1,NZ
      SCUR=Z(I)
      TEMP=DSQ+SCUR*(DB+DB+SCUR*(DC+DC+BSQ+SCUR*(BC+BC+SCUR*CSQ)))
      IF(TEMP.GT.DST) GO TO 210
      DST=TEMP
      S=SCUR
  210 CONTINUE
      T=0
      RETURN
      END
