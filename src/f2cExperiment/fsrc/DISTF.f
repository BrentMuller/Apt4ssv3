**** SOURCE FILE : M0002884.V03   ***
*
C.....FORTRAN SUBROUTINE              DISTF                16.10.78 EC
C
C              FORTRAN SUBROUTINE DISTF
C PURPOSE      DISTANCE-COMPUTATION:
C               MOD=1 PLANE/PLANE
C               MOD=2 POINT/PLANE
C               MOD=2 POINT/POINT
C
C CALL         CALL DISTF(RES,ARG1,ARG2)
C
C ARGUMENTS    RES     ARRAY FOR THE RESULT
C              ARG1    ARRAY OF FIRST ELEMENT
C              ARG2    ARRAY OF SECOND ELEMENT
C
C SUBPROGRAM   TYPE                ENTRY
C              LOGICAL FUNCTION    CKDEF
C              SUBROUTINE          ERROR
C              SUBROUTINE          UNPACK
C              DOUBLE PRECISION FUNCTION     DABS
C              SUBROUTINE          UNDEF
      SUBROUTINE DISTF(RES,ARG1,ARG2)
      DOUBLE PRECISION RES(*),ARG1(*),ARG2(*),AB,COSW
      INTEGER IENT(20)
      LOGICAL CKDEF
      DATA IENT/1,1,2,2,2,0,0,0,2,0,0,0,0,0,0,0,0,0,2,0/
      IF(CKDEF(ARG1).OR.CKDEF(ARG2))GOTO 50
      CALL UNPACK(ARG1,MODE,ISIZE,ISUB)
      IF(MODE.LT.1.OR.MODE.GT.20)GOTO 51
      IARG1=IENT(MODE)
      IF(IARG1.EQ.0)GOTO 51
      CALL UNPACK(ARG2,MODE,ISIZE,ISUB)
      IF(MODE.LT.1.OR.MODE.GT.20)GOTO 51
      IARG2=IENT(MODE)
      IF(IARG2.EQ.0)GOTO 51
      MOD=IARG1+IARG2-1
      GOTO (10,20,30),MOD
C    DISTANCE OF TWO PLANES
   10 COSW=ARG1(1)*ARG2(1)+ARG1(2)*ARG2(2)+ARG1(3)*ARG2(3)
      IF(DABS(DABS(COSW)-1.D0).GT.1.D-6)GOTO 52
      RES(1)=DABS(ARG1(4)-COSW*ARG2(4))
      RETURN
C    DISTANCE OF POINT AND PLANE
   20 AB=ARG1(4)
      IF(IARG1.EQ.2)AB=ARG2(4)
      RES(1)=ARG1(1)*ARG2(1)+ARG1(2)*ARG2(2)+ARG1(3)*ARG2(3)-AB
      RETURN
C     DISTANCE OF TWO POINTS
   30 RES(1)=DSQRT((ARG1(1)-ARG2(1))**2
     1         +(ARG1(2)-ARG2(2))**2
     2         +(ARG1(3)-ARG2(3))**2)
      RETURN
C     'ONE ELEMENT OR BOTH ARE UNDEFINED'
   50 CALL ERROR(55,'DISTF   ')
      GOTO 53
C     'WRONG INPUT'
   51 CALL ERROR(61,'DISTF   ')
      GOTO 53
C     'PLANES NOT PARALLEL'
   52 CALL ERROR(62,'DISTF   ')
   53 CALL UNDEF(RES(1))
      RETURN
      END
