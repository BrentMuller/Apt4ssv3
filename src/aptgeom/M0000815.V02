*
C.....FORTRAN SUBROUTINE             APT065...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT065
C
C PURPOSE      TO GENERATE A MATRIX DEFINED AS A POINT AND TWO
C              VECTORS BY THE FOLLOWING APT STATEMENT
C              RESULT = MATRIX/PT,V1,V2
C
C LINKAGE      CALL APT065 (RESULT, PT, V1, V2)
C
C ARGUMENTS    RESULT ARRAY TO CONTAIN THE CANONICAL FORM OF
C                     THE RESULTING MATRIX
C              PT     ARRAY CONTAINING THE CANONICAL FORM OF
C                     THE INPUT POINT
C              V1     ARRAY CONTAINING THE CANONICAL FORM OF
C                     THE FIRST INPUT VECTOR
C              V2     ARRAY CONTAINING THE CANONICAL FORM OF
C                     THE SECOND INPUT VECTOR
C
C SUBSIDIARIES TYPE               ENTRY
C              SUBROUTINE         CROSS
C              SUBROUTINE         DOTF
C              SUBROUTINE         VNORM
C              LOGICAL FUNCTION   ZVECT
C              LOGICAL FUNCTION   CKDEF
C
      SUBROUTINE APT065 (RESULT,PT,V1,V2)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION (O-Z)
      DIMENSION RESULT(12),PT(3),V1(3),V2(3)
C
C
C
      INCLUDE (TOTAL)
      LOGICAL CKDEF
      INCLUDE (DEF)
      INCLUDE (DSHAR3)
      INCLUDE (LDEF)
      INCLUDE (ISHR16)
      INCLUDE (KNUMBR)
C
      UNFLAG=CKDEF(PT).OR.CKDEF(V1).OR.CKDEF(V2)
C
C...     CALCULATE A VECTOR DEFINING Z,Y,X-AXIS
C
      CALL CROSS(V1,V2,T1(9))
C
      CALL CROSS (T1(9),V1,T1(5))
      DO 10 J=1,3
   10 T1(J)=V1(J)
C
C...  CALCULATE DISTANCES FROM ORIGIN TO YZPLANE,ZXPLANE,AND XYPLANE
C
      DO 20 J=1,9,4
      CALL VNORM(T1(J),T1(J))
      CALL DOTF(T1(J+3),T1(J),PT)
   20 T1(J+3)=-T1(J+3)
C
C...     INVERSE MATRIX
C
      CALL APT067(RESULT,T1)
      RETURN
      END
