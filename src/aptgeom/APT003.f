**** SOURCE FILE : M0000753.V03   ***
*
C
C.....FORTRAN SUBROUTINE             APT003...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT003
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A POINT
C              DEFINED AS THE INTERSECTION OF TWO LINES BY
C              THE FOLLOWING APT STATEMENT
C              RESULT = POINT/INTOF, LINE1, LINE2
C
C
C LINKAGE      CALL APT003  (RESULT, LINE1, LINE2)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM
C                      OF THE RESULTING POINT.
C              LINE1   ARRAY CONTAINING THE CANONICAL FORM
C                      OF ONE INPUT LINE.
C              LINE2   ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE OTHER INPUT LINE.
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT001
C              SUBROUTINE          APT077
C              SUBROUTINE          DEFEXT
C              REAL FUNCTION       ABS
C
      SUBROUTINE APT003 (RESULT,LINE1,LINE2)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION (O-Z)
      DIMENSION RESULT(3),LINE1(4),LINE2(4)
      DOUBLE PRECISION LINE1,LINE2
C
C
C
      INCLUDE 'TOTAL.INC'
      INCLUDE 'DEF.INC'
      INCLUDE 'DSHAR3.INC'
      INCLUDE 'ZNUMBR.INC'
      INCLUDE 'IDEF.INC'
      INCLUDE 'ISHR16.INC'
      INCLUDE 'KNUMBR.INC'
C
      DIMENSION PLL(4,3)
C
C...     TRANSFORM CANONICAL FORM TO LOCAL SYSTEM
C
      CALL APT077(LINE1,  2)
      DO 10 I=1,4
   10 PLL(I,1)=L(I)
      CALL APT077(LINE2,  3)
      DO 11 I=1,4
   11 PLL(I,2)=L(I)
      PLL(1,3)=Z0
      PLL(2,3)=Z0
      PLL(3,3)=Z1
      PLL(4,3)=Z0
C
C....   COMPUTE SIMULTANEOUS SOLUTION OF 3 PLANE EQUATIONS
C
      CALL APT075 (RESULT,PLL)
      IF(IERROR.EQ.K52) GO TO 20
      CALL APT001(RESULT)
      GO TO 30
   20 IERROR=K4
      CALL DEFEXT(RESULT)
   30 RETURN
      END
