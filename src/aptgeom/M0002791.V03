*
C.....FORTRAN SUBROUTINE  ....APT008         8/68                    SR
C.....FORTRAN SUBROUTINE             APT008...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT008
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A POINT
C              DEFINED AS THE INTERSECTION OF A LINE AND A
C              CONIC BY THE FOLLOWING APT STATEMENT
C              RESULT = POINT/****, INTOF, LINE1, G1
C              **** = XLARGE, YLARGE, XSMALL OR YSMALL
C
C LINKAGE      CALL APT008 (RESULT, M, LINE1, G1)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM OF THE
C                      RESULTING POINT
C              M       INTEGER EQUIVALENT OF THE MODIFIER;
C                      1 = XLARGE
C                      2 = YLARGE
C                      4 = XSMALL
C                      5 = YSMALL
C              LINE1   ARRAY CONTAINING THE CANONICAL FORM OF THE
C                      INPUT LINE
C              G1      ARRAY CONTAINING THE CANONICAL FORM OF THE
C                      INPUT CONIC.
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT077
C              SUBROUTINE          APT079
C              SUBROUTINE          DEFEXT
C              SUBROUTINE          ERROR
C              SUBROUTINE          APT001
C              REAL FUNCTION       ABS
C              REAL FUNCTION       SQRT
C
      SUBROUTINE APT008 (RESULT,MN,LINE1,G1)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION(O-Z)
      DIMENSION RESULT(3),LINE1(4),G1(10)
      DOUBLE PRECISION LINE1
C
C
C
C
      INCLUDE (TOTAL)
      INCLUDE (DEF)
      INCLUDE (DSHAR3)
      INCLUDE (ZNUMBR)
      INCLUDE (LDEF)
      INCLUDE (IDEF)
      INCLUDE (KNUMBR)
C
      EQUIVALENCE(S1,SC(1)),(S2,SC(2)),(S3,SC(3)),(S4,SC(4)),(S5,SC(5)),
     1(S6,SC(6)),(S7,SC(7)),(S8,SC(8))
C
C...     TRANSFORM CANONICAL FORMS AND CHECK INPUT VALIDITY
C
      M = MN
      CALL APT077 (LINE1,K5)
      CALL APT079 (G1,0)
      IF ((DABS(G(3)).GT.Z1EM5).OR.(DABS(G(5)).GT.Z1EM5).OR.
     1    (DABS(G(6)).GT.Z1EM5).OR.(DABS(G(10)).GT.Z1EM5)) GO TO 60
C
C...     DETERMINE A POINT ON THE LINE
C
      S1  =L(4)*L(1)
      S2  =L(4)*L(2)
C
C...     DETERMINE COEFFICIENTS S(3),S(4),S(5) OF QUADRATIC FORMULA
C
      S3  =G(1)*L(2)*L(2)+G(2)*L(1)*L(1)-Z2*G(7)*L(2)*L(1)
      S4  =G(1)*S1  *L(2)-G(2)*L(1)*S2  -G(7)*L(1)*S1
     1      +G(7)*L(2)*S2  +G(8)*L(2)-G(9)*L(1)
      S5  =G(1)*S1  *S1  +G(2)*S2  *S2  +G(4)
     1       +Z2*(G(7)*S1  *S2  +G(8)*S1  +G(9)*S2  )
C
C...     DETERMINE THE RADICAL OF THE QUADRATIC FORMULA
C
      S6=S4*S4-S3*S5
      IF (S6  .LT.-Z1EM5) GO TO 70
      IF (DABS(Z1EM5-DABS(S6)).LT.Z1EM5) S6=Z0
      S6=DSQRT(S6)
      IF (DABS(S3).LT.Z1EM7) GO TO 80
C
C...     DETERMINE THE TWO ROOTS OF THE QUADRATIC FORMULA
C
      S7=(-S4+S6)/S3
      S8=(-S4-S6)/S3
C
C...     SELECT THE CORRECT ROOT DEPENDING ON M
C
      GO TO (10,20,20,10,20),M
   10 IF(L(2)/S3) 140,30,100
   20 IF(L(1)/S3) 100,40,140
C
C...     VERTICAL LINE WITH XLARGE OR XSMALL
C
   30 CALL ERROR (K8,'APT008  ')
      GO TO 20
C
C...     HORIZONTAL LINE WITH YLARGE OR YSMALL
C
   40 CALL ERROR (K9,'APT008  ')
      GO TO 10
C
C...     INPUT SURFACE UNDEFINED
C
   50 UNFLAG=.TRUE.
   55 CALL DEFEXT (RESULT)
      GO TO 500
C
C...     CONIC SURFACE CONTAINS VALUES FOR Z COORDINATE
C
   60 IERROR=K16
      GO TO 50
C
C...     LINE AND CONIC DO NOT INTERSECT
C
   70 IERROR=K15
      GO TO 50
C
C...     S(3)=0 - QUAD IS X=-S(5)/S(4)  S(4) LT S(5)*Z1EM7-NO INTERSEC.
C
   80 IF (DABS(S4).LT.(Z1EM7*S5)) GO TO 70
      S7=-S5/S4
      GO TO 110
C
C...     DETERMINE LARGE OR SMALL
C
  100 IF (M-K3) 110,110,130
  110 RESULT(1)=S1  +L(2)*S7
      RESULT(2)=S2  -L(1)*S7
  120 CALL APT001 (RESULT)
      GO TO 500
  130 RESULT(1)=S1  +L(2)*S8
      RESULT(2)=S2  -L(1)*S8
      GOTO 120
  140 IF (M-K3) 130,130,110
  500 CONTINUE
      RETURN
       END
