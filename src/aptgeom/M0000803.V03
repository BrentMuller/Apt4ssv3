*
C
C.....FORTRAN SUBROUTINE             APT053...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT053
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A SPHERE
C              DEFINED AS PASSING THROUGH FOUR GIVEN POINTS
C              BY THE FOLLOWING APT STATEMENT
C              RESULT = SPHERE/PT1, PT2, PT3, PT4
C
C LINKAGE      CALL APT053 (RESULT, PT1, PT2, PT3, PT4)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM
C                      OF THE RESULTING SPHERE
C              PT1     ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE FIRST INPUT POINT
C              PT2     ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE SECOND INPUT POINT
C              PT3     ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE THIRD INPUT POINT
C              PT4     ARRAY CONTAINING THE CANONICAL FORM
C                      OF THE FOURTH INPUT POINT
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT075
C              LOGICAL FUNCTION    CKDEF
C              SUBROUTINE          DEFEXT
C              REAL FUNCTION       SQRT
C
      SUBROUTINE APT053 (RESULT,PT1,PT2,PT3,PT4)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION (O-Z)
      DIMENSION RESULT(4),PT1(3),PT2(3),PT3(3),PT4(3),ST(3,3),PT(4,3)
      LOGICAL CKDEF
C
C
C
      INCLUDE (TOTAL)
      INCLUDE (DEF)
      INCLUDE (DSHAR3)
      INCLUDE (ZNUMBR)
      INCLUDE (LDEF)
      INCLUDE (IDEF)
      INCLUDE (ISHR16)
      INCLUDE (KNUMBR)
C
      EQUIVALENCE (ST(1,1),SC(1)),(PT(1,1),P(1))
C
      DATA K57/57/
C...     MOVE INPUTS TO SCRATCH STORAGE, CHECK INPUT
C
      UNFLAG=CKDEF(PT4)
      DO 10 I=1,3
      ST(1,I) =PT1(I)
      ST(2,I) =PT2(I)
      ST(3,I) =PT3(I)
   10 UNFLAG =UNFLAG.OR.CKDEF(ST(I,1))
C
C...     FORM EQUATION COEFFICIENTS
C
      DO 30 I=1,3
      PT (4,I)=Z0
C
      DO 20 J=1,3
      PT(I,J) =ST(J,I) -PT4(I)
   20 PT(4,I) =PT(4,I) +ST(I,J)**2 -PT4(J)**2
   30 R(4)=R(4)+PT4(I)**2
C
C...     SOLVE FOR SPHERE EQUATION COEFFS.
C
      CALL APT075(R,PT)
C
C...     CHECK FOR COPLANAR POINTS
C
      IF (IERROR.GE. K1) IERROR = K57
C
C...     COMPUTE RADIUS TERM
C
      DO 40 I=1,3
      R(I)=R(I)/Z2
   40 R(4) =R(4) +R(I)*(R(I) -Z2*PT4(I))
      R(4)=DSQRT(R(4))
      CALL DEFEXT(RESULT)
      RETURN
       END
