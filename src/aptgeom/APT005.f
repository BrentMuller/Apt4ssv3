**** SOURCE FILE : M0000755.V03   ***
*
C
C.....FORTRAN SUBROUTINE             APT005...              3/1/68   GK
C
C              FORTRAN SUBROUTINE APT005
C
C PURPOSE      TO GENERATE THE CANONICAL FORM OF A POINT
C              DEFINED AS THE INTERSECTION OF TWO CIRCLES
C              BY THE FOLLOWING APT STATEMENT
C              RESULT = POINT/****, INTOF, CIRC1, CIRC2
C                 **** = XLARGE, YLARGE, XSMALL, YSMALL
C
C LINKAGE      CALL APT005 (RESULT, M, CIRC1, CIRC2)
C
C ARGUMENTS    RESULT  ARRAY TO CONTAIN THE CANONICAL FORM
C                      OF THE RESULTING POINT
C              M       INTEGER EQUIVALENT OF THE MODIFIER
C                      1 = XLARGE
C                      2 = YLARGE
C                      4 = XSMALL
C                      5 = YSMALL
C              CIRC1   ARRAY CONTAINING THE CANONICAL FORM
C                      OF ONE INPUT CIRCLE
C              CIRC2   ARRAY CONTAINING THE CANONICAL FORM OF THE
C                      OTHER INPUT CIRCLE
C
C SUBSIDIARIES TYPE                ENTRY
C              SUBROUTINE          APT083
C              SUBROUTINE          APT044
C              SUBROUTINE          APT004
C
      SUBROUTINE APT005 (RESULT,MN,CIRC1,CIRC2)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION(O-Z)
      DIMENSION RESULT(3),CIRC1(7),CIRC2(7)
C
C
C
      INCLUDE 'TOTAL.INC'
      INCLUDE 'DEF.INC'
      INCLUDE 'DSHAR3.INC'
      INCLUDE 'ZNUMBR.INC'
      INCLUDE 'ISHR16.INC'
      INCLUDE 'KNUMBR.INC'
C
      DIMENSION RSAVE(4)
C
C...     TRANSFORM CANONICAL FORMS AND CHECK INPUT VALIDITY
C
      CALL APT083(CIRC1,K12)
      DO 10 I=1,7
   10 SC(I)=C(I)
      CALL APT083(CIRC2,K13)
C
C...     GET CANONICAL FORM OF LINE (APT044) AND THEN POINT (APT004)
C
      SC(8) =Z2*(C(1) -SC(1))
      SC(9) = Z2* (C(2) -SC(2))
      SC(10) =SC(7)**2 -C(7)**2 +C(1)**2-SC(1)**2 +C(2)**2-SC(2)**2
      DO 20 I=1,4
   20 RSAVE(I)=R(I+3)
      CALL APT044 (C1(2),SC(8),SC(9),Z0,SC(10))
      DO 30 I=1,4
   30 R(I+3)=RSAVE(I)
      CALL APT004 (RESULT,MN,C1(2),CIRC1)
       RETURN
       END
