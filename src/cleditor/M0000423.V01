*
C.....FORTRAN SUBROUTINE              APT306                12/67
C PURPOSE      TO GENERATE THE PRODUCT MATRIX OF TWO GIVEN
C              3X4 MATRICES.
C LINKAGE      CALL APT306 (RESULT, M1, M2)
C ARGUMENTS    RESULT    ARRAY TO CONTAIN THE ELEMENTS OF THE
C                        PRODUCT MATRIX
C              M1        ARRAY CONTAINING THE ELEMENTS OF THE
C                        PREMULTIPLIER MATRIX
C              M2        ARRAY CONTAINING THE ELEMENTS OF THE
C                        POSTMULTIPLIER MATRIX
C SUBSIDIARIES NONE
      SUBROUTINE APT306 (RESULT,M1,M2)
      IMPLICIT DOUBLE PRECISION (A-H),DOUBLE PRECISION (O-Z)
C     THIS ROUTINE MULTIPLIES TWO MATRICES OF THREE ROWS AND FOUR
C     COLUMNS
      DIMENSION RESULT(3,4),M1(3,4),M2(3,4),GM1(4,4),GM2(4,4),GM3(4,4)
      DOUBLE PRECISION M1,M2
      DO 50  J=1,4
      DO 50  I=1,3
      GM1 (I,J) =M1(I,J)
      GM2 (I,J) =M2(I,J)
   50 CONTINUE
      I=4
      DO 100 J=1,3
      GM1 (I,J)=0.0D0
      GM2 (I,J)=0.0D0
  100 CONTINUE
      GM1 (4,4)=1.0D0
      GM2 (4,4)=1.0D0
      DO 190 I=1,4
      DO 190 J=1,4
      GM3 (I,J)=0.0D0
      DO 190 K=1,4
      GM3 (I,J)= GM3 (I,J)+GM1 (I,K)*GM2 (K,J)
  190 CONTINUE
      DO 230 J =1,4
      DO 230 I =1,3
      RESULT (I,J) =GM3 (I,J)
  230 CONTINUE
      RETURN
       END
