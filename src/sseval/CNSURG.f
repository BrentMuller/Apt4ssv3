**** SOURCE FILE : M0000617.V02   ***
*
      SUBROUTINE CNSURG(U,V,B,SPV,IFLAG,MODE)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C--  MODULAR.  PURPOSE IS TO GENERATE SURFACE POINT AND DIFFERENTIAL
C--  ELEMENTS ON A COONS PATCH (B) FROM INPUT COORDINATES U,V.
C--  U,V = INPUT SURFACE COORDINATES
C--  B   = INPUT COONS PATCH COEFFICIENTS
C--  IFLAG = INPUT ARRAY WHICH RECORDS THE TYPE OF EACH 16-COMPONENT
C--           MATRIX IN THE ARRAY B( 1=CONSTANT,2=LINEAR,3,4=FULL)
C--  MODE  = INPUT.  MODE=0 MEANS ONLY SURFACE POINT REQD.
C--               OTHERWISE FIRST AND SECOND ORDER DIFFERENTIAL ELEMENTS
C--                ARE ALSO REQUIRED
C--  SPV   = OUTPUT  SPV(1-4)= PROJECTIVE COORDINATES OF SURFACE POINT
C--               SPV(5-8) = P.C. OF FIRST DERIV WRT U
C--                SPV(9-12)=  P.C. OF FIRST DERIV WRT V
C--                SPV(13-16)= P.C. OF SECOND DERIV WRT U
C--                SPV(17-20) = P.C. OF MIXED PARTIAL WRT U,V
C--                SPV(21-24) = P.C. OF SECOND DERIV WRT TO V
      LOGICAL IMODE
      DIMENSION B(64),SPV(32),IFLAG(4)
      DIMENSION F(12),G(12),T(12)
C---     POINTERS FOR RAPID EVALUATION OF SPV THRU PRODUCTS OF T AND G.
      DIMENSION NT(6),NG(6)
      DATA NT/1,5,1,9,5,1/
      DATA NG/1,1,5,1,5,9/
C
C
      DATA ZERO,ONE,SMAL/0.0D0,1.0D0,1.0D-12/
C
      IMODE=.FALSE.
      IF(MODE.EQ.0) IMODE=.TRUE.
C------ EVALUATE COONS BLEND FUNCTIONS AND THEIR DERIVATIVES
C------ FOR PARAMETER VALUES U AND V
      CALL BLENDF(U,F)
      CALL BLENDF(V,G)
C------ EVALUATE THE PROJECTIVE SURFACE POINT AND PARTIAL DERIVATIVE
C------ COORDINATES.  EACH COORDINATE SET(X,Y,Z,W) IS EVALUATED
C------ ON SUCCESSIVE PASSES THRU THE 10 LOOP AND SPECIAL SPEED
C------ ROUTES ARE TAKEN FOR A LINEAR OR CONSTANT COORDINATE
C------ MATRIX
      DO 10 I=1,4
      K=IFLAG(I)
      GO TO (100,200,300,300),K
  300 CONTINUE
      N=16*I-16
      KLIM=9
      IF(IMODE) KLIM=1
      DO 310 L=1,KLIM,4
      T(L  )=F(L)*B(N+ 1)+F(L+1)*B(N+ 2)+F(L+2)*B(N+ 3)+F(L+3)*B(N+ 4)
      T(L+1)=F(L)*B(N+ 5)+F(L+1)*B(N+ 6)+F(L+2)*B(N+ 7)+F(L+3)*B(N+ 8)
      T(L+2)=F(L)*B(N+ 9)+F(L+1)*B(N+10)+F(L+2)*B(N+11)+F(L+3)*B(N+12)
      T(L+3)=F(L)*B(N+13)+F(L+1)*B(N+14)+F(L+2)*B(N+15)+F(L+3)*B(N+16)
  310 CONTINUE
      SPV(I) = T(1)*G(1)+T(2)*G(2)+T(3)*G(3)+T(4)*G(4)
      IF(IMODE) GO TO 10
C
      LV=0
      DO 320 L=1,5
      LV=LV+4
      M=NT(L+1)
      LG=NG(L+1)
      SPV(I+LV)=T(M)*G(LG)+T(M+1)*G(LG+1)+T(M+2)*G(LG+2)+T(M+3)*G(LG+3)
  320 CONTINUE
      GO TO 10
C
  100 SPV(I)=B(16*I-15)
      IF(IMODE) GO TO 10
      DO 110 L = 4,20,4
  110 SPV(I +L) = ZERO
      GO TO 10
C
  200 CONTINUE
      N=16*I-15
      T(1) = B(N)
      T(2) = B(N +1)-B(N)
      T(3) = B(N +4)-B(N)
      T(4) = B(N +5)-B(N +4)-T(2)
      SPV(I) = T(1) +T(2)*U +(T(3) +T(4)*U)*V
      IF(IMODE) GO TO 10
      SPV(I +4) = T(2) +T(4)*V
      SPV(I +8) = T(3) +T(4)*U
      SPV(I +12) = ZERO
      SPV(I +16) = T(4)
      SPV(I +20) = ZERO
   10 CONTINUE
C
      IF(IFLAG(4).EQ.1) GO TO 400
C
C--  INTERPOLATE EUCLIDEAN QUANTITIES FROM PROJECTIVE QUANTITIES
C---     PATCH IS OF RATIONAL FORM SO COMPUTE DERIVATIVES BY RATIOS.
C---     FIRST EVALUATE FIXED W DERIVATIVES AND RATIOS.
      IF(IMODE) GO TO 360
C---     T(1)=WSQ, 2=WU, 3=WV, 4=TWU, 5=TWV, 6=WUU, 7=WUV, 8=WVV
      T(1)=SPV(4)**2
      T(2)=SPV(8)/T(1)
      T(3)=SPV(12)/T(1)
      T(4)=(SPV(8)+SPV(8))/SPV(4)
      T(5)=(SPV(12)+SPV(12))/SPV(4)
      T(6)=SPV(16)/T(1)
      T(7)=SPV(20)/T(1)
      T(8)=SPV(24)/T(1)
C---     LOOP TO EVALUATE RATIONAL DERIVATIVES FOR X,Y,Z COMPONENTS
      DO 350 L=1,3
C---     PARTIAL OF F WITH RESPECT TO U =FU.
      SPV(L+4)=SPV(L+4)/SPV(4)-SPV(L)*T(2)
C---     PARTIAL OF F WITH RESPECT TO V
      SPV(L+8)=SPV(L+8)/SPV(4)-SPV(L)*T(3)
C---     SECOND PARTIAL OF F WITH RESPECT TO U.
      SPV(L+12)=-T(4)*SPV(L+4)+SPV(L+12)/SPV(4)-SPV(L)*T(6)
C---     SECOND PARTIAL OF F WITH RESPECT TO V.
      SPV(L+20)=-T(5)*SPV(L+8)+SPV(L+20)/SPV(4)-SPV(L)*T(8)
C---     MIXED SECOND PARTIAL OF F WITH RESPECT TO U AND V.
      SPV(L+16)=-(T(5)*SPV(L+4)+T(4)*SPV(L+8))/2.0+
     X          SPV(L+16)/SPV(4)-SPV(L)*T(7)
C---     NOTE THAT THE ORDER OF THE ABOVE CALCULATIONS IS SENSITIVE
  350 CONTINUE
C
  360 CONTINUE
      DO 370 L=1,3
      SPV(L)=SPV(L)/SPV(4)
  370 CONTINUE
C
  400 CONTINUE
C---     SET INDICATOR VALUES FOR QUANTITIES COMPUTED
      IF(IMODE) GO TO 501
      DO 500 I=8,24,4
  500 SPV(I)=ONE
  501 CONTINUE
      SPV(4)=ONE
C
      RETURN
      END
