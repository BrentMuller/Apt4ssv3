**** SOURCE FILE : M0001635.V04   ***
*
      SUBROUTINE CNSURF(U,V,B,SPV,IFLAG,MMODE)
C...................................................................
C..PURPOSE   GIVEN PATCH AND PARAMETRIC COORDINATES COMPUTE THE PATCH
C..          POINTS AND DERIVATIVES.  SEARCH LOGIC IS USED TO ATTEMPT
C..          TO FIND A NON-ZERO SURFACE NORMAL WHEN THAT NORMAL IS TOO
C..          SMALL.  WHEN PATCH COORDINATES ARE OUTSIDE THE UNIT SQUARE
C..          DATA IS COMPUTED ON THE UNIT BORDER AND EXTRAPOLATED BY A
C..          LINEAR BLEND FORMULA.
C..ARGUMENTS
C..   U,V -  INPUT  PARAMETRIC U,V COORDINATES OF PATCH.
C..   B   -  INPUT PATCH COEFFICIENT ARRAY FOR BICUBIC PATCH AND PATCH
C..          TYPE FLAG ( IN B(1) ) FOR GENCUR PATCH
C..   SPV -  OUTPUT  POINT AND DERIVATIVES OF PATCH AT U,V POSITION
C..          SPV(1-3)=POINT, SPV(5-8)=DP/DU, SPV(9-11)=DP/DV,
C..          SPV(13-15)=DDP/DUU, SPV(17-19)=DDP/DUV, SPV(21-23)=DDP/DVV
C..          SPV(25-27)= DP/DU X DP/DV,  SPV(29-31)=NORM OF SPV(25-27)
C..          SPV(4,8,12,..)=1 IF ARRAY IS SET, =0 IF NOT SET.
C..   IFLAG  INPUT MATRIX FLAGS FOR BICUBIC PATCH AND LOCAL POINTERS
C..          FOR GENCUR PATCH CURVES.
C..   MMODE  INPUT  =0 MEANS CALCULATE PATCH POINT ONLY.
C..                 =1 MEANS CALCULATE POINT AND DERIVATIVES(SPV)
C
C..CALLED BY APT109 BALLOC BALSRF DGEOM  PCHPRC PUVLOC SRFCOM SSPICT
C..CALLS     CNSURG CNSURH CROSS  DMIN1  DMAX1  DOTV   DSQRT
C..ERRORS    NO PROGRAMMED ERROR MODES.
C..RESTRICTIONS
C..          SEARCH LOGIC TO RETRIEVE A NON-ZERO NORMAL MAY NOT
C..          SUCCEED.  IN THAT CASE NORMAL IS SET TO ZERO.  ALSO
C..          SURFACE NORMAL ORIENTATION MAY BE REVERSED IN AREAS
C..          OF COLLAPSING EXTENSIONS.
C..LOCAL VARIABLES
C..   AINC   VARIABLE TO CONTROL THE PARAMETRIC INCREMENT FOR SEARCHING
C..   MODE   VARIABLE TO CONTROL NUMBER OF DERIVATIVES CALCULATED
C..          =MMODE, BUT =1 ALWAYS ON LINEAR EXTENSIONS OF SURFACE
C..   NCT    THE NUMBER OF SEARCH ATTEMPTS
C..   SPW    A BUFFER TO HOLD CURRENT SURFACE PT AND DERIVATIVES WHILE
C..          SEARCHING FOR A NON ZERO NORMAL OR EXTRAPOLATING DATA ON
C..          EXTENSIONS
C..   U1,V1  THE CLOSEST POINT IN THE UNIT SQUARE TO U,V
C..   VSMAL  FIXED VARIABLE USED TO JUDGE WHEN A VECTOR LENGTH IS SMALL
C...................................................................
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION B(64),SPV(32),IFLAG(4)
      DIMENSION SPW(32)
      CHARACTER ENAME*8
C
      INCLUDE 'BLANKCOM.INC'
C
      DATA ZERO,ONE/0.0D0,1.0D0/
      DATA VSMAL,SMAL,HALF/1.0D-9,1.0D-6,0.499999D0/
C
C---/STEP 1/   PREPARATION
C---      SET U1,V1 TO CLOSEST POINT IN UNIT SQUARE TO U,V
C---      WHEN U,V IS OUTSIDE THE UNIT SQUARE THE SURFACE POINT
C---      AND DERIVATIVES ARE EXTRAPOLATED FROM DATA ON THE BORDER.
      U1=U
      V1=V
      U1=DMIN1(ONE,U1)
      U1=DMAX1(ZERO,U1)
      V1=DMIN1(ONE,V1)
      V1=DMAX1(ZERO,V1)
      MODE=MMODE
      IEXT=0
      IF(U.EQ.U1.AND.V.EQ.V1) GO TO 5
C---      U,V IS OUTSIDE THE UNIT SQUARE, EXTRAPOLATION REQUIRED.
      IEXT=1
      MODE=1
    5 CONTINUE
C
C--- NCT IS THE COUNTER WHICH KEEPS TRACK OF THE NUMBER OF TRIALS
C--- WITHIN CNSURF WHENEVER TROUBLE IS ENCOUNTERED IN FINDING A NORMAL
      NCT=0
C---    AINC IS STARTING FACTOR FOR ALTERATION OF U-V VALUES
C---    WHILE SEARCHING FOR A NON DEGENERATE SURFACE NORMAL
      AINC=0.000000001
C---      SAVE U1,V1 VALUES, AS THEY MAY BE CHANGED BY A SEARCH
      U1SAVE=U1
      V1SAVE=V1
C
    1 CONTINUE
C---/STEP 2/   PATCH DERIVATIVES
C
C---    EXTRACT SURFACE POINT AND DERIVATIVES AT U,V VALUES
C---     INTERPOLATE SURF PT AND DERIVS AS RATIONAL BICUBIC.
      IF(IFLAG(1).GT.9) GOTO 7
      CALL CNSURG(U1,V1,B,SPW,IFLAG,MODE)
      GOTO 6
C---     INTERPOLATE SURF PT AND DERIVS AS GENCUR PATCH
    7 IF(IFLAG(2).EQ.0) GOTO 8
      CALL CNSURH(U1,V1,B,SPW,IFLAG,MODE)
      GOTO 6
C---     INTERPOLATE SURF PT AND DERIVS AS FOREIGN PATCH
    8 CALL HOLFRM(B(1),ENAME,1,8,NWD)
      CALL EVAL(ENAME,U1,V1,CANON(IFLAG(1)),SPW,MODE)
C
C---      SAVE SURFACE POINT ON FIRST PASS
    6 IF(NCT.NE.0) GO TO 3
      DO 2 I=1,3
    2 SPV(I)=SPW(I)
      SPV(4)=ONE
    3 CONTINUE
C
C---      SKIP NORMAL CHECKING WHEN ONLY POINT IS DESIRED
      IF(MMODE.EQ.0) GO TO 400
C---/STEP 3/   SIGNIFICANCE OF DERIVATIVES
C---    EXAMINE SURFACE NORMAL FOR NON DEGENERACY
      CALL CROSS(SPW(5),SPW(9),SPW(25))
      CALL DOTV(D3,SPW(25),SPW(25))
      D3=DSQRT(D3)
      IF(D3.GT.VSMAL) GO TO 310
C---    SURFACE NORMAL MAGNITUDE IS SMALL SO EXAMINE FURTHER
C---    FIND MAGNITUDES OF TANSPL AND CRSSPL VECTORS
      CALL DOTV(D1,SPW(5),SPW(5))
      D1=DSQRT(D1)
      CALL DOTV(D2,SPW(9),SPW(9))
      D2=DSQRT(D2)
C---    IF EITHER TANGENT VECTOR IS SMALL, FIND NEW U,V
      IF(D1.LT.VSMAL.OR.D2.LT.VSMAL) GO TO 309
C---    FINALLY CHECK ANGULAR SPREAD BETWEEN TANSPL AND CRSSPL
      IF(D3.GT.SMAL*D1*D2) GO TO 310
C
  309 CONTINUE
C---/STEP 4/   ALTER COORDINATES
C---    OBTAIN A NEW U,V AND TRY FOR A CLEARER SURFACE NORMAL
      NCT=NCT+1
C---    QUIT AFTER 8 TRIALS
      IF(NCT.GT.8) GO TO 310
      FAC=ONE-VSMAL*10.0D0**NCT
      U1=(U1SAVE-HALF)*FAC+HALF
      V1=(V1SAVE-HALF)*FAC+HALF
C---    RECYCLE TO CHECK WHETHER NEW NORMAL IS DEFINED
      GO TO 1
C
C---/STEP 5/   CONSOLIDATE PATCH DERIVATIVES
  310 CONTINUE
C---      RESTORE U1,V1 AND COMPUTE UNIT NORMAL
      U1=U1SAVE
      V1=V1SAVE
      IF(D3.EQ.ZERO) D3=ONE
      DO 311 I=1,3
      SPW(I)=SPV(I)
      SPW(I+28)=SPW(I+24)/D3
  311 CONTINUE
C
C---      FINAL STEP, EXTRAPOLATE SURFACE PT AND DERIVS IF POINT
C---      IS OUTSIDE THE UNIT SQUARE.
  400 CONTINUE
      IF(IEXT.NE.0) GO TO 410
      IF(MMODE.EQ.0) GO TO 490
C---      TRANSFER DERIVATIVES TO SPV ARRAY
      DO 405 I=5,32
  405 SPV(I)=SPW(I)
      GO TO 490
C
  410 CONTINUE
C---/STEP 6/   DERIVATIVES EXTRAPOLATED TO PATCH EXTENSION.
      DU=U-U1
      DV=V-V1
C---      U1 IS A FUNCTION OF U.  U1P IS RATE OF CHANGE OF U1 WRT U.
C---      SIMILARLY FOR V1 AND V1P.
      U1P=ZERO
      IF(U.EQ.U1) U1P=ONE
      V1P=ZERO
      IF(V.EQ.V1) V1P=ONE
      SPV(4)=ONE
C---      EXTRAPOLATION FORMULA IS AS FOLLOWS
C---      SPV = SPW + DU*SPW-U +DV*SPW-V + DU*DV*SPW-UV
      DO 430 I=1,3
      SPV(I)=SPW(I)+DU*SPW(I+4)+DV*(SPW(I+8)+DU*SPW(I+16))
      IF(MMODE.EQ.0) GO TO 430
      SPV(I+4)=SPW(I+4)+DV*SPW(I+16)
      SPV(I+8)=SPW(I+8)+DU*SPW(I+16)
      SPV(I+12)=U1P*SPW(I+12)
      SPV(I+16)=SPW(I+16)
      SPV(I+20)=V1P*SPW(I+20)
  430 CONTINUE
      IF(MMODE.EQ.0) GO TO 490
      CALL CROSS(SPV(5),SPV(9),SPV(25))
      CALL DOTV(D1,SPV(25),SPV(25))
      D1=DSQRT(D1)
      IF(D1.EQ.ZERO) D1=ONE
      DO 440 I=1,3
  440 SPV(I+28)=SPV(I+24)/D1
  490 CONTINUE
C---      SET FLAGS FOR DERIVATIVES
      IF(MMODE.EQ.0) GO TO 999
      DO 495 I=8,32,4
  495 SPV(I)=ONE
C
  999 CONTINUE
      RETURN
      END
