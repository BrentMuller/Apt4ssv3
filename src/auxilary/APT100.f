**** SOURCE FILE : M0002807.V05   ***
*
C.....FORTRAN SUBROUTINE               APT100    3/69      SR,DE
      SUBROUTINE APT100(IFLAG,ARG)
C
C     PURPOSE       TO MOVE POINTS AND PATERNS USED AS ARGUMENTS
C                   IN THE DEFINITION OF A RANDOM PATERN TO THE LDA
C                   STAGING AREA, AND PERFORM ERROR CHECKS
C
C     CALLING SEQ   CALL APT100(IFLAG,ARG)
C
C     ARGUMENTS     FLAG  INDICATOR SHOWING TYPE OF PROCESSING DESIRED
C                   ARG   A POINT OR PATTERN WHOSE CANONICAL FORM IS TO
C                   BE MOVED TO THE LDA STAGING AREA
C
C     RESTRICTIONS  CURRENT VERSION CANNOT HANDLE
C                   NESTED PATTERN DEFINITIONS
C
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
      LOGICAL CKDEF
C
      INCLUDE 'BLANKCOM.INC'
C
      DIMENSION ARG(3)
      INTEGER ITEMP(2),BCDF
      DOUBLE PRECISION TEMP
      EQUIVALENCE (TEMP,ITEMP(1))
C
C...                BRANCH IF NOT INITIALIZATION CALL
      IF(IFLAG.NE.0)GO TO 100
C                             INITIALIZE COMMON STORAGE INDEX
      INEXT=5
C                             INITIALIZE FIRST WORD OF COMMON STORAGE
C                             AREA TO NO ERROR CONDITION
      BCANON(1)=0.0
      GO TO 900
C...                IF ERROR HAS ALREADY OCCURRED IN THIS DEFINITION,
C...                RETURN
  100 IF(BCANON(1).LT.0.0)GO TO 900
      IF(CKDEF(ARG))GO TO 800
C...                BRANCH IF PATERN TYPE ARG
      IF(IFLAG.EQ.2)GO TO 200
C...                PROCESS POINT TYPE ARG
C                             INSERT ALPHANUMERIC IDENTIFIER
      ITEMP(1) = BCDF('ZPOI')
      ITEMP(2) = BCDF('NT  ')
      BCANON(INEXT)=TEMP
C                             INSERT CANONICAL FORM
      DO 110 I=1,3
      J=INEXT+I
      BCANON(J)=ARG(I)
  110 CONTINUE
C                             UPDATE STORAGE INDEX
      INEXT=INEXT+4
      GO TO 900
C
C...                PROCESS PATERN ARG
  200 CALL APT094(2,ARG,BCANON(INEXT))
      IF(CKDEF(ARG))GO TO 800
C                             INSERT ALPHANUMERIC IDENTIFIER
      ITEMP(1) = BCDF('ZPAT')
      ITEMP(2) = BCDF('RN  ')
      BCANON(INEXT)=TEMP
C                             IF ARGUMENT PATERN IS ALSO RANDOM, ISSUE
C                             DIAGNOSTIC AND STORE ERROR FLAGS
      REAL=BCANON(INEXT+1)
      IF(REAL.NE.4.0)GO TO 220
      CALL ERROR(82,'APT100  ')
  800 BCANON(1)=-1.0
      GO TO 900
C                             UPDATE STORAGE INDEX
  220 INT=BCANON(INEXT+2)+1.00001
      INEXT=INEXT+INT
  900 RETURN
      END
