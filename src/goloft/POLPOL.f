**** SOURCE FILE : M0012381.V01   ***
*
      SUBROUTINE POLPOL(POED1,M1,N1,POED2,M2,N2,TOL,SP,IRC)
C
C  TO CHECK THE INTERFERENCE BETWEEN A PLANE AND A POLYHEDRON
C      POED1: I : POLYHEDRAL DATA OF SURFACE 1
C      POED2: I : POLYHEDRAL DATA OF SURFACE 2
C      TOL  : I : INTERSECTION TOLERANCE
C      SP   : O : INTERSECTION NEAR POINT
C      IRC  : O : RETURNCODE : NO INTERSECTION: IRC=0
C
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
      DOUBLE PRECISION POED1(3,M1,N1),TOL,POED2(3,M2,N2),SP(3),DIST(5),D
      DOUBLE PRECISION P1(3),P2(3),P3(3),V1(3),V2(3),V3(3),PL(4)
      DOUBLE PRECISION FACE(3,6),PF(3)
      EQUIVALENCE (FACE(1,1),P1(1))
      EQUIVALENCE (FACE(1,2),V1(1))
      EQUIVALENCE (FACE(1,3),P2(1))
      EQUIVALENCE (FACE(1,4),V2(1))
      EQUIVALENCE (FACE(1,5),P3(1))
      EQUIVALENCE (FACE(1,6),V3(1))
C
C
      IRC=0
      DISTMI=1.D20
      N1M1=N1-1
      M1M1=M1-1
      DO 50 L=1,N1M1
      DO 50 K=1,M1M1
C
C   COMPUTE INDICES OF TRIANGLE FACES ON DS PATCH
      K1=K
      L1=L
      L2=L+1
      K3=K+1
      DO 50 M=1,2
      K2=K+M-1
      L3=L+2-M
C
C  EXTRACT FACE CORNER POINTS
      DO 40 N=1,3
      P1(N)=POED1(N,K1,L1)
      P2(N)=POED1(N,K2,L2)
      P3(N)=POED1(N,K3,L3)
      V1(N)=P2(N)-P1(N)
      V2(N)=P3(N)-P2(N)
   40 V3(N)=P1(N)-P3(N)
C
C  GENERATE PLANE COEFFICENTS
      CALL CROSS(V2,V1,PL)
      SEC=DSQRT(PL(1)**2+PL(2)**2+PL(3)**2)
      PL(1)=PL(1)/SEC
      PL(2)=PL(2)/SEC
      PL(3)=PL(3)/SEC
      PL(4)=PL(1)*P1(1)+PL(2)*P1(2)+PL(3)*P1(3)
C
C  DISTANCE BETWEEN FACES ON DS PATCH AND POINTS OF PS PATCH
      DO 10 J=1,N2
      DO 10 I=1,M2
      DISTI=POED2(1,I,J)*PL(1)+POED2(2,I,J)*PL(2)+POED2(3,I,J)*PL(3)
     * -PL(4)
C
C   MINIMUM DISTANCE SELECTION
      IF(DISTMI.LT.DABS(DISTI)) GOTO 2
C
C  CALCULATE FOOT POINT ON PLANE
      PF(1)=POED2(1,I,J)-DISTI*PL(1)
      PF(2)=POED2(2,I,J)-DISTI*PL(2)
      PF(3)=POED2(3,I,J)-DISTI*PL(3)
C
C   CALCULATE IF FOOT POINT IS IN PLANE
      CALL INTFA1 (FACE,PL,PF,IRC1)
      IF(IRC1.EQ.0) GOTO 2
C
C  DISTANCE TO PLANE
      DISTMI=DABS(DISTI)
      II=I
      JJ=J
C
C   CROSS OF PLANE IN V-DIRECTION ?
    2 IF(J.EQ.1) GOTO 5
      IF(DISTI*DIST(I).GT.0.D0) GOTO 5
      CALL INTFA2 (FACE,PL,POED2(1,I,J-1),POED2(1,I,J),IRC1)
      IF(IRC1.EQ.0) GOTO 5
C
C   CROSS OR TUCH POINT FOUND
      II=I
      JJ=J
      GOTO 20
C
C   CROSS OF PLANE IN U-DIRECTION ?
    5 IF(I.EQ.1) GOTO 7
      IF(DISTI*DIST(I-1).GT.0.D0) GOTO 7
      CALL INTFA2 (FACE,PL,POED2(1,I-1,J),POED2(1,I,J),IRC1)
      IF(IRC1.EQ.0) GOTO 7
C
C   CROSS OR TUCH POINT FOUND
      II=I
      JJ=J
      GOTO 20
C
C   STORE DISTANCE
    7 DIST(I)=DISTI
   10 CONTINUE
   50 CONTINUE
C
C   RETURN IF THE SURFACES ARE NOT CLOSE ENOUGH
      IF(TOL*100.D0.LT.DISTMI) RETURN
C
C  STORE NEAR POINT
   20 DO 30 K=1,3
   30 SP(K)=POED2(K,II,JJ)
      IRC=1
C
      RETURN
      END
