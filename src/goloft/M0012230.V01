*
      SUBROUTINE CPLDST(SPV,PLANE,TH,IWAS,DIST,IRET)
C
C   ROUTINE TO CALCULATE THE DISTANCE OF A CUTTER TO/ON/PAST POINT
C    TO A PLANE
C
C
      IMPLICIT DOUBLE PRECISION(A-H),DOUBLE PRECISION(O-Z)
C
C  CONTROL OF TEST-OUTPUT
      COMMON/SSPRT/SSPRT,SSTEST
      LOGICAL SSPRT,SSTEST
C
C  INPUT EVALUATION VALUES OF CUTTER POSITION
      DOUBLE PRECISION SPV(52)
C
C  INPUT PLANE
      DOUBLE PRECISION PLANE(4)
C
C  INPUT THICKNES
      DOUBLE PRECISION TH
C
C  INPUT TO/ON/PAST TYPE (2/3/4)
      INTEGER IWAS
C
C  OUTPUT DISTANCE
      DOUBLE PRECISION DIST
C
C  OUTPUT RETURN CODE (=0 IF OK)
      INTEGER IRET
C
C  TOOL POINT,HELP VECTORS
      DOUBLE PRECISION TP(3),VT(3),VT2(3)
C
C  GOLOFT/PLOTFT COMMON
C
C  USED CUTTER SEGMENT
      INTEGER JCSEG
C
C  NUMBER OF VALID CUTTER SEGMENTS
      INTEGER NCSEG
C
C  CUTTER SEGMENT IN TLLDAT ARRAYS
      INTEGER ICSEG(10)
C
C  NATURE OF SEGMENT
      INTEGER NLIOCI(10)
C
C  COMPONENTS OF SEGMENT AND UNIT NORMAL POINTING INTO THE CUTTER
      DOUBLE PRECISION COSAL(10)
      DOUBLE PRECISION SINAL(10)
C
C  DISTANCE PARALLEL TO THE CUTTER AXIS
      DOUBLE PRECISION CUTY(10)
C
C  DISTANCE TO THE CUTTER AXIS
      DOUBLE PRECISION CUTX(10)
C
C  ELEMENT LENGTH
      DOUBLE PRECISION CUTL(10)
C
      COMMON/PLGOL/ID(24),OBNAM1,JCSEG,NCSEG,ICSEG,NLIOCI,
     *             COSAL,SINAL,
     *             CUTY,CUTX,CUTL
C
C AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
C
C CHECK TOOL POSITION INFORMATION
      IF(IWAS.GE.2.AND.IWAS.LE.4) GOTO 5
      IRET=MAX0(IRET,1)
      IF(.NOT.SSTEST)RETURN
C
C  WRONG TOOL AXIS INFORMATION.
      CALL AERR(22223,'CPLDST  ')
      CALL BAD(1,0,'IWAS',IWAS)
      RETURN
C
C  MOVE TOOL TIP
    5 DO 10 I=1,3
   10 TP(I)=SPV(32+I)
C
C  ON-CONDITION ?
      IF(IWAS.EQ.3) GOTO 90
C
C  COMPUTE COSIN OF ANGLE BETWEEN TOOL AXIS AND PLANE
      COSANG=SPV(37)*PLANE(1)
     *      +SPV(38)*PLANE(2)
     *      +SPV(39)*PLANE(3)
      IF(IWAS.EQ.4) COSANG=-COSANG
C
C  FIND TOOL SEGMENT
      DO 20 I=1,NCSEG
      IF(NLIOCI(I).EQ.1) GOTO 20
      JCSEG=I
      IF(COSAL(I).LE.COSANG) GOTO 25
   20 CONTINUE
C
C  COMPUTE VECTOR PERPTO AXIS AND PERPTO PLANE NORMAL
   25 CALL CROSS(PLANE,SPV(37),VT)
C
C  COMPUTE LENGTH OF THIS VECTOR
      VLL=VT(1)*VT(1)+VT(2)*VT(2)+VT(3)*VT(3)
C
C  TAKE ONLY TOOL TIP, IF VLL TOO SMALL
      IF(VLL.LE.1.D-10) GOTO 90
      VLL=DSQRT(VLL)
C
C  COMPUTE VECTOR PERPENTICULAR TO TOOL AXIS AND POINTING TO PLANE
      CALL CROSS(VT,SPV(37),VT2)
C
C  INVERT VLL FOR PAST CONDITION
      IF(IWAS.EQ.4) VLL=-VLL
C
C  COMPUTE FACTOR FOR TO AND PAST CONDITION
      FACT=1.D0
      IF(IWAS.EQ.4) FACT=-1.D0
C
C  COMPUTE CUTTER POINT
      DO 40 I=1,3
   40 TP(I)=TP(I)
     *           +SPV(36+I)*CUTY(JCSEG)
     *           +VT2(   I)*CUTX(JCSEG)/VLL
     *           -PLANE( I)*(CUTL(JCSEG)+TH)*FACT
C
C  COMPUTE DISTANCE
   90 DIST=TP(1)*PLANE(1)
     *    +TP(2)*PLANE(2)
     *    +TP(3)*PLANE(3)
     *    -PLANE(4)
C
      RETURN
      END
