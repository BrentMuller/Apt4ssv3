*
C
C              FORTRAN SUBROUTINE MCXTRN
C
C LINKAGE      SUBROUTINE MCXTRN(IBR,INDEX,INFO)
C
C          SUBSIDIARIES                  CALLED BY
C          TYPE          ENTRY           TYPE          ENTRY
C          SUBROUTINE    HELP            SUBROUTINE    ALARM
C          SUBROUTINE    ALARM           SUBROUTINE    IEPLOG
C          SUBROUTINE    AREAD           SUBROUTINE    INPAT
C          SUBROUTINE    AWRITE          SUBROUTINE    INPUT
C          SUBROUTINE    TAPOP           SUBROUTINE    MACREC
C          SUBROUTINE    CHREAD          SUBROUTINE    MOTION
C          SUBROUTINE    CHWRIT          INTEGER FCT.  NUMB
C                                        SUBROUTINE    OPPAIR
C                                        SUBROUTINE    PRO026
C                                        SUBROUTINE    PRO027
C                                        SUBROUTINE    RECOG
C                                        SUBROUTINE    RESERV
C                                        SUBROUTINE    RESRED
C
      SUBROUTINE MCXTRN(IBR,INDEX,INFO)
C
      IMPLICIT INTEGER (A-Z)
C
C
C        1.    NAME TABLE AND TABLE LIMITS
C
      INCLUDE (NAMETB)
C NAMTBL:      INTEGER CORRESPONDING TO NAME TABLE ENTRY
C CNAMTB:      NAME TABLE OR DICTIONARY
C NUMBST:      NAMTBL INDEX OF BEGINNING OF NUMBER SECTION
C
C       14.    I. L. LIST OPTION FLAGS
C
      COMMON/PRT/PRT
      LOGICAL ASSEMB
      DIMENSION PRT(7)
C ASSEMB:      SET TRUE INDICATES ASSEMBLY APPROACH (CONTRL/ASMBLE)
      EQUIVALENCE (ASSEMB,PRT(4))
C
C       22.    MACRO PROCESSING VARIABLES
C
      COMMON/MACXX1/MACXX1
      DIMENSION MACXX1(71)
C MBPTR:       POINTER TO LAST MBREC ENTRY
      EQUIVALENCE (MBPTR,MACXX1(30))
C
C       23.    MACRO PROCESSING ARRAYS
C
      COMMON/MACXX2/MACXX2
      DIMENSION MACXX2(902),MBUFF(350,2),MACRTB(200),MBREC(2)
C MBREC:       IDENTIFIES THE RECORDS(350 WORD BLK) CURRENTLY IN MBUFF
      EQUIVALENCE (MBREC(1),MACXX2(1))
C BUFF:        BUFFER ARRAY IN ORDER TO READ OR WRITE ON EXTERNAL FILE
      EQUIVALENCE(MBUFF(1,1),MACXX2( 3))
C MACRTB:      BUFFER ARRAY OF THE EXTERNAL FILE
      EQUIVALENCE (MACRTB(1),MACXX2(703))
C
C       33.    SYMBOLIC FILE DEFINITIONS
C
      INCLUDE (SYMFIL)
C MACTXT:          EXTERNAL FILE  OF MACRO TEXT
C XNUM  :          OVERFLOW NUMBER TABLE
C
C       41.    NUMBER TABLE OVERFLOW VARIABLES
C
      COMMON/NTBL/NTBL
      DIMENSION NTBL(6)
C NMOD:        NUMBER OF ENTRIES PERMITTED IN INTERNAL NUMBER TABLE
      EQUIVALENCE (NMOD,NTBL(1))
C NCRNT:       ACTUAL NUMBER TABLE BLOCK - INITIALLY 1
      EQUIVALENCE (NCRNT,NTBL(2))
C NNDX:        CONVERSION BETWEEN ABS. AND VIRTUAL NUMBER TABLE POINTER
      EQUIVALENCE (NNDX,NTBL(3))
C NLAST:       LAST NEW BLOCK NUMBER OF NUMBER TABLE ON THE EXTERN FILE
      EQUIVALENCE (NLAST,NTBL(4))
C*
C ARGUMENTS    IBR     SET TO 1 FOR STORE, 2 FOR RETRIEVE
C              INDEX   POINTER TO REFERENCED MACRO TEXT WORD
C              INFO    DATA TO BE STORED OR RETRIEVED
C**
      IF(IBR.GT.2)GO TO 500
C
C...     DETERMINE RECORD NUMBER AND WORD NUMBER
C
      ITMP = (INDEX-1)/350
      IREC = ITMP + 1
      IF (IREC.LT.200) GO TO 10
      CALL ALARM(35,0,8,'MCXTRN  ')
      CALL HELP
   10 IWRD = INDEX-350*ITMP
C...  IS THIS RECORD IN THE BUFFERS
      IF (IREC.EQ.MBREC(MBPTR)) GO TO 100
      MBPTR = 3-MBPTR
      IF(IREC.EQ.MBREC(MBPTR)) GO TO 100
C...  WRITE OLD BUFFER TO EXTERNAL FILE
      I = MBREC(MBPTR)
      IF(MACRTB(I).NE.0) GO TO 20
      NREC = 0
      CALL AWRITE(MACTXT,NREC,MBUFF(1,MBPTR),350,IRET)
      MACRTB(I)=NREC
C...  RECORD INDICATOR INDICATES NEW RECORD NUMBER
   20 MBREC(MBPTR) = IREC
      IF(MACRTB(IREC).GT.0) GO TO 30
      IF(IBR.EQ.2) CALL ALARM(35,0,8,'MCXTRN  ')
      GO TO 100
C...  READ IN RECORD
   30 CALL AREAD (MACTXT,MACRTB(IREC),MBUFF(1,MBPTR),350,IRET)
  100 IF(IBR.EQ.2) GO TO 200
C...  STORE INFO
      MBUFF(IWRD,MBPTR)=INFO
      IF(MACRTB(IREC).NE.0) MACRTB(IREC) = 0
      GO TO 400
C...  READ INFO
  200 INFO=MBUFF(IWRD,MBPTR)
      GO TO 400
C
C
C...     NUMBER TABLE OVERFLOW PROCESSING
C
  500 IBRTMP = IBR-2
      GO TO (530,540,550,560),IBRTMP
C
C...  WRITE FIRST PART OF TABLE
C
  530 L=(NMOD*12)/2
      J=0
C... 4TH ARGUMENT OF CHWRIT IS NUMBER OF BYTES
      CALL CHWRIT(XNUM,J,CNUMTB(NUMBST),L,IRET)
      NLAST = NLAST+1
      NNDX = NNDX +(NMOD/2)
      L = NUMBST + (NMOD/2) - 1
      DO 544 I=NUMBST,L
      CNUMTB(I) = ' '
  544 CONTINUE
      GO TO 400
C
C.... WRITE SECOND PART OF TABLE
C
  540 IF(ASSEMB) GO TO 805
      J=0
      I=NUMBST + (NMOD/2)
      L = (NMOD*12)/2
C... 4TH ARGUMENT OF CHWRIT IS NUMBER OF BYTES
      CALL CHWRIT(XNUM,J,CNUMTB(I),L,IRET)
      NLAST = NLAST+1
      NNDX = NNDX + (NMOD/2)
      NCRNT = 2
      GO TO 400
C
C.... READ IN BLOCK CONTAINING INDEX
C
  550 L = NMOD/2
      IF (INDEX.LT.NUMBST) GO TO 400
      ITMP=(INDEX-NUMBST)/L
      INDEX = INDEX - ITMP*L
      IF(ITMP.EQ.NLAST) GO TO 400
      INDEX = INDEX + L
      IF(ITMP+1.EQ.NCRNT) GO TO 400
      NREC = ITMP + 1
      I = NUMBST + L
      L = L*12
C... 4TH ARGUMENT OF CHREAD IS NUMBER OF BYTES
      CALL AREAD (XNUM,NREC,CNUMTB(I),L,IRET)
      NCRNT = NREC
      GO TO 400
C
C
C.... WRITE OUT FINAL RECORD
C
  560 CALL TAPOP (XNUM,-2)
  400 RETURN
  805 CALL ALARM(36,0,8,'MCXTRN  ')
      CALL HELP
      RETURN
      END
