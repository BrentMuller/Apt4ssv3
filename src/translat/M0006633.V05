*
C
C              FORTRAN SUBROUTINE GETTMP
C
C LINKAGE      INTEGER FUNCTION GETTMP(MODE)
C
C          SUBSIDIARIES                  CALLED BY
C          TYPE          ENTRY           TYPE          ENTRY
C          INTEGER FCT.  NAME            SUBROUTINE    BOOLIF
C          SUBROUTINE    RESRVZ          SUBROUTINE    INPAT
C          SUBROUTINE    SHIFT           SUBROUTINE    ISSCOD
C          SUBROUTINE    BINBCD          SUBROUTINE    LARGES
C                                        SUBROUTINE    LCANON
C                                        SUBROUTINE    LOOP
C                                        SUBROUTINE    PRO020
C                                        SUBROUTINE    PRO021
C                                        SUBROUTINE    PRO026
C                                        SUBROUTINE    PRO027
C                                        SUBROUTINE    RECOG
C                                        SUBROUTINE    RECOGN
C                                        SUBROUTINE    SSDEF
C                                        SUBROUTINE    SUBCOD
C
      INTEGER FUNCTION GETTMP(MODE)
C
      IMPLICIT INTEGER (A-Z)
C
C        1.    NAME TABLE AND TABLE LIMITS
C
      INCLUDE (NAMETB)
C NAMTBL:      INTEGER CORRESPONDING TO NAME TABLE ENTRY
C CNAMTB:      NAME TABLE OR DICTIONARY
C
C        3.    METALINGUISTIC VARIABLE CLASS CODES
C
      COMMON/CODE/CODE
      DIMENSION CODE(150),TEMP(2)
C TEMP:        (1)=CODE CLASS 17000,(2)=NAMTBL INDEX OF 'TEMP  '
      EQUIVALENCE (TEMP(1),CODE(33))
C
C        6.    GHOST STACK FOR OBJECT TIME TEMPORARY ALLOCATION
C              LENGTH AND CURRENT NUMBER OF ACTIVE TEMPORARY STORAGE
      COMMON/TMP/TXXXX
      DIMENSION TXXXX(2,32)
C*
C PURPOSE      THIS ROUTINE RETURNS THE NAME TABLE (NAMTBL)
C              INDEX FOR THE CURRENT TEMPORARY NAME OF THIS
C              MODE.
C
C ARGUMENTS    MODE    THE MODE OF THE SURFACE REQUIRING
C                      TEMPORARY STORAGE.
C
C ARGUMENTS    MODE    THE MODE OF THE SURFACE REQUIRING
C                      TEMPORARY STORAGE.
C
      CHARACTER SYMBOL*6,SYMI*8,BLK*1
      DATA BLK/' '/
C**
C...     INCREMENT COUNTER FOR THIS MODE
C
      TXXXX(2,MODE)=TXXXX(2,MODE)+1
C
C...     GENERATE NAME OF TEMPORARY LOCATION
C
      SYMBOL = BLK
C
C...           START SYMBOL WITH A $
C
      CALL SHIFT(SYMBOL,'$')
C
C...     IS MODE .GT. 9
C
      IF (MODE/10 .EQ. 0) GO TO 10
C
C...     YES. CONVERT TENS DIGIT OF MODE TO BCD AND SHIFT INTO SYMBOL
C
      CALL BINBCD(MODE/10,SYMI,COUNT)
      CALL SHIFT(SYMBOL,SYMI)
C
C...     CONVERT UNITS DIGIT TO BCD AND SHIFT INTO SYMBOL
C
   10 CALL BINBCD(MOD(MODE,10),SYMI,COUNT)
      CALL SHIFT(SYMBOL,SYMI)
C
C...           SHIFT T$ INTO SYMBOL
C
      CALL SHIFT(SYMBOL,'T')
      CALL SHIFT(SYMBOL,'$')
C
C...     IS COUNTER FOR THIS MODE .GT. 9
C
      IF (TXXXX(2,MODE)/10 .EQ. 0)  GO  TO  20
C
C...     YES. CONVERT TENS DIGIT TO BCD AND SHIFT INTO SYMBOL
C
      CALL BINBCD(TXXXX(2,MODE)/10,SYMI,COUNT)
      CALL SHIFT(SYMBOL,SYMI)
C
C...     CONVERT UNITS DIGIT OF POINTER TO BCD AND SHIFT INTO SYMBOL
C
   20 CALL BINBCD(MOD(TXXXX(2,MODE),10),SYMI,COUNT)
      CALL SHIFT(SYMBOL,SYMI)
C
C...     FIND NAMTBL INDEX OF NAME JUST CREATED AND STORE AS GETTMP
C
      GETTMP=NAME(SYMBOL)
C
C...     IS FIRST COLUMN OF NAMTBL ZERO.
C...     IF SO, THEN THIS IS THE FIRST APPEARENCE OF THIS NAME.
C...     IF NOT, IT HAS BEEN USED BEFORE, SO RETURN.
C
      IF (NAMTBL(GETTMP).NE.0) RETURN
C
C...     FIRST APPEARENCE - GENERATE RESRV CODE AND SET CORRECT CLASS
C...     AND MODE IN COLUMN 1 OF NAMTBL. THEN RETURN.
C
      CALL RESRVZ(GETTMP,MODE,TXXXX(1,MODE),1)
      NAMTBL(GETTMP) = TEMP(1)+MODE
      RETURN
       END
