PARTNO TEST04 XYPLAN TEST
REMARK      TEST RUN ON SSX3A SYSTEM--- OCTOBER 1973
$$
$$ ********************************************
$$ *                                          *
$$ *   THIS TEST OK IN SSV3  -  09-JUN-1989   *
$$ *                                          *
$$ ********************************************
$$
$$   THIS TEST OK IN SSV1 - 07-JUL-1982
$$               AND SSV2 - 02-OCT-1986
$$
CLPRNT
NOPOST
$$   THIS PART PROGRAM TESTS SURFACE DEFINITION AND CUTTER MOTION
$$   INSIDE AND OUTSIDE THE BOUNDARY OF A FULL XYPLAN MESH AND A RULED
$$   XYPLAN MESH
P1=POINT/0,0,0
P2=POINT/1,0,1
P3=POINT/2,0,1
P4=POINT/3,0,0
Q1=POINT/0,1,0
Q2=POINT/1,1,0.5
Q3=POINT/2,1,0.5
Q4=POINT/3,1,0
R1=POINT/0,2,0
R2=POINT/1,2,0.6
R3=POINT/2,2,0.6
R4=POINT/3,2,0.2
S1=POINT/0,3,0.1
S2=POINT/1,3,0.6
S3=POINT/2,3,0.4
S4=POINT/3,3,0
$$  SS1 IS A GENERAL XYPLAN MESH
SS1=SSURF/SMESH,XYPLAN,SPLINE,P1,P2,P3,P4,SPLINE,Q1,Q2,Q3,Q4,$
                      SPLINE,R1,R2,R3,R4,SPLINE,S1,S2,S3,S4
$$  SS2 IS A RULED XYPLAN MESH
SS2=SSURF/SMESH,XYPLAN,SPLINE,P1,P2,P3,P4,SPLINE,S1,S2,S3,S4
PINS=POINT/0.3,0.3,0.5
POUT=POINT/-0.3,-0.3,0
X0=LINE/0,0,0,1
Y0=LINE/0,0,1,0
X3=LINE/3,0,3,1
Y3=LINE/0,3,1,3
CUTTER/0.25,0.125,0,0,0,0,0.5
ROUND=MACRO/PP,SS,GOSS
GOTO/PP
INDIRV/0,0,-1
GO/X0,SS,Y0
GOSS/X0,Y3
GORGT/Y3,X3
GORGT/X3,Y0
GORGT/Y0,X0
GOTO/PP
TERMAC
$$ CUT INSIDE THE BOUNDARY OF EACH SURFACE
CALL/ROUND,PP=PINS,SS=SS1,GOSS=GORGT
CALL/ROUND,PP=PINS,SS=SS2,GOSS=GORGT
$$   CUT OUTSIDE THE BOUNARY OF EACH SURFACE
CALL/ROUND,PP=POUT,SS=SS1,GOSS=GOLFT
CALL/ROUND,PP=POUT,SS=SS2,GOSS=GOLFT
FINI
