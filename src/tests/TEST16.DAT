PARTNO TEST16  SWARF 5 AXIS TESTS FOR SCULPTURED SURFACES( C E DRAYTON)
REMARK      TEST RUN ON SSX3A SYSTEM--- OCTOBER 1973
$$ ********************************************
$$ *                                          *
$$ *   THIS TEST OK IN SSV3  -  13-JUN-1989   *
$$ *                                          *
$$ ********************************************
$$
$$   THIS TEST OK IN SSV1 - 06-JUL-1982
$$               AND SSV2 - 03-NOV-1986
$$
CLPRNT
MULTAX
INTOL/.002
OUTTOL/.002
CUTTER/1,0,0,0,0,0,1
ORG=POINT/0,0
L1=LINE/ORG,ATANGL,-40
L2=LINE/ORG,ATANGL,-20
LX=LINE/XAXIS
L3=LINE/ORG,ATANGL, 20
L4=LINE/ORG,ATANGL, 40
XYPL=PLANE/0,0,1,0
XY4=PLANE/0,0,1,4
CP=CIRCLE/0,0,14
CR=CIRCLE/0,0,18
CQ=CIRCLE/0,0,20
ZSURF/(PLANE/0,0,1,10)
P1=POINT/XLARGE,INTOF,L1,CP
P2=POINT/XLARGE,INTOF,L2,CP
P3=POINT/XLARGE,INTOF,LX,CP
P4=POINT/XLARGE,INTOF,L3,CP
P5=POINT/XLARGE,INTOF,L4,CP
ZSURF/(PLANE/0,0,1,0)
Q1=POINT/XLARGE,INTOF,L1,CQ
Q2=POINT/XLARGE,INTOF,L2,CQ
Q3=POINT/XLARGE,INTOF,LX,CQ
Q4=POINT/XLARGE,INTOF,L3,CQ
Q5=POINT/XLARGE,INTOF,L4,CQ
ZSURF/(PLANE/0,0,1,6)
R1=POINT/XLARGE,INTOF,L1,CR
R2=POINT/XLARGE,INTOF,L2,CR
R3=POINT/XLARGE,INTOF,LX,CR
R4=POINT/XLARGE,INTOF,L3,CR
R5=POINT/XLARGE,INTOF,L4,CR
S1=SSURF/SMESH,XYZ,$
SPLINE,P1,P2,P3,P4,P5,$
SPLINE,Q1,Q2,Q3,Q4,Q5
S2=SSURF/SMESH,XYZ,$
SPLINE,P1,P2,P3,P4,P5,$
SPLINE,R1,R2,R3,R4,R5,$
SPLINE,Q1,Q2,Q3,Q4,Q5
S3=RLDSRF/CP,P1,P5,P3,CQ,Q1,Q5,Q3
KUT=MACRO/HI=0,SSF,PSF,Z=0
TLAXIS/(VECTOR/UNIT,-4.8,3.8,10)
FROM/22,-4,Z
$$  TLAXIS/3,2,.5,HI,90 - THIS IS NOW AN UNACCEPTED FORM
TLAXIS/PARLEL,2,.5,HI
GO/TO,SSF,TO,PSF,TO,L1
GOBACK/SSF,TO,LX
GOTO/22,-4,Z
TERMAC
$$   SCULPTURED SURFACE RULED CUT
CALL/KUT,SSF=S1,PSF=XYPL
$$   SCULPTURED 5 AXIS SWARF CUT
CALL/KUT,SSF=S2,PSF=XYPL
$$  NORMAL RLDSRF CUT
   CALL/KUT,SSF=S3,PSF=XYPL
FINI
