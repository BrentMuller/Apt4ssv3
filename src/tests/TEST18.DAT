PARTNO TEST18  EXAMPLES OF SYNTHETIC CURVE DEFINITIONS
REMARK      TEST RUN ON SSX3A SYSTEM--- OCTOBER 1973
$$ ********************************************
$$ *                                          *
$$ *   THIS TEST OK IN SSV3  -  13-JUN-1989   *
$$ *                                          *
$$ ********************************************
$$
$$   THIS TEST OK IN SSV1 - 05-JUL-1982
$$               AND SSV2 - 29-SEP-1986
$$
$$  THIS PROGRAM EXERCISES MANY OF THE OPTIONS OF A SCULPTURED CURVE
$$  DEFINITIONS-  BOTH SPLINE AND CURSEG
$$   THE SYNTHETIC OR SCULPTURED OR SPACE CURVE IS A NEW GEOMETRIC
$$  CONCEPT IN APT
$$ ITS DEVELOPMENT HAS ONLY NOW BEGUN
$$  THE DEFINITIONS YOU SEE HERE, WHILE VERY POWERFUL ARE ONLY A
$$  BEGINNING
$$  THE MAIN IDEA OF THE CURVE FOR SCULPTURED SURFACES IS THAT YOU
$$  MAY APPROACH SURFACE DEFINTION PROBLEMS BY  FIRST DEFINING SIMPLER
$$  CURVES AND THEN LATER USING THESE CURVES TO DEFINE SURFACES
$$  AT PRESENT , THERE IS NO OPTION TO USE A CURVE FOR TOOL CONTROL
$$  ALTHO MANY USEFUL CONCEPTS HAVE BEEN SUGGESTED
$$  AT PRESENT YOU MAY DEFINE EITHER A TWISTED SPLINE CURVE IN SPACE
$$ (SPLINE) OR ELSE A BOUNDED CONIC ARC AS A SCULPTURED CURVE
$$  IN LATER RELEASES, IT IS PLANNED THAT DIVERSE CURVES CAN BE LINKED
$$  TOGETHER TO FORM A SINGLE COMPOSITE CURVE OF MANY DIFFERENT ARCS
$$  ONE ADVANTAGE OF THIS IS THAT ONE ARELEM MOTION STATEMENT WOULD BE
$$  SUFFICIENT TO MILL AROUND A COMPOSITE(OR SYNTHETIC) CONTOUR
$$  AT PRESENT YOU CAN PERFORM ONLY TWO OPERATIONS ON AN SCURV AFTER
$$  IT HAS BEEN DEFINED--  EITHER MAKE A CYLINDRICAL SCULPTURED SURFACE
$$  OR ELSE A SURFACE OF REVOLUTION
CLPRNT
      A=20
      B=15
      FN=MACRO/X,Y,P,V,N
$$  MACRO TO GENERATE VALUES FOR A POINT ON AN ELLIPTIC ARC
      Y=B*SQRTF(1-(X*X)/(A*A))
      P=POINT/X,Y
      N=VECTOR/(X/(A*A)),(Y/(B*B)),0
      V=VECTOR/(Y/(B*B)),(-X/(A*A)),0
      TERMAC
      X1=0
      X2=5
      X3=10
      X4=15
      X5=20
$$  FIVE POINTS , TANGENT VECTORS AND NORMAL VECTORS ARE DEFINED OFF
$$  A QUADRANT OF AN ELLIPSE WITH MAJOR AXIS  20 UNITS AND MINOR AXIS
$$  15 UNITS
      CALL/FN,X=X1,Y=Y1,P=P1,V=V1,N=N1
      CALL/FN,X=X2,Y=Y2,P=P2,V=V2,N=N2
      CALL/FN,X=X3,Y=Y3,P=P3,V=V3,N=N3
      CALL/FN,X=X4,Y=Y4,P=P4,V=V4,N=N4
      CALL/FN,X=X5,Y=Y5,P=P5,V=V5,N=N5
$$  THE FOLLOWING DEFINTIONS EXERCISE MANY OF THE CURSEG OPTIONS
$$  FOR AN SCURV DEFINTION
$$  THE INPUT FOR SPLINE OR CURSEGS ARE EQUIVALENT
$$  HOWEVER CURSEG SPECIFIES THAT THE INPUT DATA MUST BE FITTED WITH A
$$  SINGLE CONIC ARC BEGINNING AT THE FIRST POINT AND ENDING AT THE LAST
$$  IF THREE CONDITIONS ARE INPUT,THEN A CIRCULAR ARC WILL BE DEFINED
       C1=SCURV/CURSEG,P1,P3,P5
$$  NOTICE C2 IS ALSO CIRCULAR, BUT DIFFERENT FROM C1
       C2=SCURV/CURSEG,P1,P4,P5
$$  AGAIN THREE CONDITIONS-- TWO POINTS AND AN END TANGENT VECTOR
$$  DETERMINE A CIRCLE
       C3=SCURV/CURSEG,P1,TANSPL,V1,P5
$$  A NORMAL VECTOR AT THE END IS JUST AS EFFECTIVE AS A TANGENT
       C4=SCURV/CURSEG,P1,P5,NORMAL,N5
$$  IN ORDER TO REPRODUCE THE ORIGINAL ELLIPTICAL ARC EXACTLY THREE
 $$ POINTS(INCLUDING TWO END POINTS) AND TWO END VECTORS MUST BE INPUT
       C5=SCURV/CURSEG,P1,TANSPL,V1,P3,P5,TANSPL,V5
$$  AGAIN, A NORMAL VECTOR IS JUST AS EFFECTIVE AS A TANGENT VECTOR
$$  FOUR POINTS WILL BE INTERPOLATED WITH A SINGLE CONIC
$$  HOWEVER IT MAY NOT BE IDENTICAL WITH THE ORIGINAL ELLIPSE
       C7=SCURV/CURSEG,P1,P2,P4,P5
$$  AGAIN C8 IS DIFFERENT FROM C7 BECAUSE DIFFERENT PTS ARE USED
       C8=SCURV/CURSEG,P1,P3,P4,P5
$$  AT PRESENT, FIVE POINTS WILL NOT EXACTLY FIT THE ORIGINAL CONIC
$$  ONLY THE THREE POINT , TWO VECTOR FORM GUARANTEES THIS
       C9=SCURV/CURSEG,P1,P2,P3,P4,P5
$$  C10 AND C11 ARE OTHER APPROXIMATIONS TO THE ELLIPSE WITH AN ELLIPSE
       C10=SCURV/CURSEG,P1,TANSPL,V1,P4,P5
       C11=SCURV/CURSEG,P1,P2,TANSPL,V2,P3
$$     NOTE THAT A CONIC ARC OF MORE THAN 179.99 DEGREE SPAN SHOULD NOT
$$     BE DEFINED AT PRESENT( THE DEFINITION WILL FAIL)
      Q1=POINT/1,0,0
      Q2=POINT/0,1,0
      Q3=POINT/-1,0,0
      Q4=POINT/ (COSF(179)),(SINF(179)),0
       C12=SCURV/CURSEG,Q1,Q2,Q3 $$ THIS WILL FAIL
$$  WHEN Q4 IS NEAR 180 BUT NOT EQUAL, THE DEFINTION IS OK
       C13=SCURV/CURSEG,Q1,Q2,Q4 $$ THIS SHOULD BE DEFINED
$$-------
$$     THE REMAINING DEFINTIONS USE THE SPLINE OPTION OF SCURV
$$   WHERE AS THE CURSEG FITS ALL OF THE DATA WITH A SINGLE CONIC ARC
$$  (AND MAY NOT PASS THRU SOME OF THE INTERIOR DATA EXACTLY),
$$  THE SPLINE OPTION INTERPOLATES A CUBIC ARC BETWEEN EACH TWO CONSEC-
$$  UTIVE POINTS OF THE SPLINE.  WHEREAS ALL THE BASIC CURSEG
$$  POINTS AND TANGENT VECTORS MUST LIE IN A PLANE, THE SPLINE
$$  POINTS MAY BE TWISTED IN SPACE
$$  NOTE THAT THE FIVE POINTS ON THE ELLIPSE GENERATE 4 CUBIC ARCS
      S1=SCURV/SPLINE,P1,P2,P3,P4,P5
$$ NORMAL AND TANGENT CONSTRAINTS ARE OBSERVED EXACTLY AT EVERY POINT
$$  OF A SPLINE
$$  -- UNLESS OTHERWISE STATED, EACH POINT HAS A WEIGHT VALUE1 WHICH
$$  MEANS YOU ARE CERTAIN OF THE POINT AND IT SHOULD NOT BE MOVED
$$  IF YOU ASSIGN A FRACTIONAL WEIGHT ( BETWEEN 0 AND 1) TO A POINT
$$  THEN IT WILL BE MOVED OR RELAXED INTO A SMOOTHER CONFIGURATION
$$  WITH THE REMAINING POINTS.
$$ IF A WEIGHT IS INTRODUCED AFTER THE WORD SPLINE BUT BEFORE
$$  THE FIRST POINT, THEN IT IS INTERPRETED AS A GLOBAL WEIGHT
$$  AND ALL POINTS ARE CHANGED FROM A WEIGHT OF 1 TO THE NEW VALUE.
$$  THIS WILL CAUSE A VERY BUMPY CURVE TO FLUCTUATE MUCH LESS
$$  NOTICE A NOISY POINT IS THROWN INTO THE MIDDLE OF THIS SPLINE
       S3=SCURV/SPLINE,WEIGHT,0.001,P1,P2,P3,(POINT/15,15,0),P5
$$  IN THE NEXT STATEMENT A WEIGHT IS INTRODUCED AFTER THE ODD POINT
$$  THIS MEANS ALL POINTS ARE GOING TO MOVE EXCEPT THE ODD POINT
    S5=SCURV/SPLINE,WEIGHT,0.001,P1,P2,P3,(POINT/15,15,0),WEIGHT,1.,P5
$$  THE POINTS FROM MOVING MORE THAN THE LIMIT SPECIFIED AMOUNT
$$  AS WITH WEIGHTS, LIMITS CAN BE GLOBAL OR LOCAL
      S6=SCURV/SPLINE,WEIGHT,0.001,LIMIT,.1,P1,P2,P3,(POINT/15,15,0), $
         LIMIT,1.,WEIGHT,1.,P5
$$  FINALLY , IF VECTOR CONSTRAINTS ARE ADDED, THEN THEY ARE OBSERVED
$$  EXACTLY, EVEN THOUGH THE ORIGINAL POINTS MAY MOVE
       S8=SCURV/SPLINE,WEIGHT,0.001,P1,P2,P3,(POINT/15,15,0),TANSPL, $
                  (VECTOR/1,0,0),P5
FINI
