PARTNO  TEST21  TEST OF SSX4 GEOMETRIC CONSTRUCTION, IKUO OYAKE,OKI
$$  SSX4  NEW GEOMETRIC CONSTRUCTION CAPABILITY TEST
$$ ********************************************
$$ *                                          *
$$ *   THIS TEST OK IN SSV3  -  14-JUN-1989   *
$$ *                                          *
$$ *   VECTOR/INTOF,SSURF,...,NORMAL          *
$$ *   HAS BEEN CORRECTED - CAR: 0003         *
$$ *                                          *
$$ *   WARNING 5129  OK                       *
$$ ********************************************
$$
$$     LARGELY OK SSV1 - 06-JUL-1982
$$
$$     BUT GAVE ODD ANSWERS FOR  VECTOR/INTOF,SCURV,...,NORMAL
$$                                            SSURF
$$     RETURNING THE COORDINATES OF THE POINT
$$
$$     OK SSV2 - 01-OCT-1986
$$
$$     TRAPPED ERRONEOUS VECTOR DEFINITIONS GIVING ERROR 5158
$$     FOR VS1,VS2,VS7,CV2 AND CV4
$$
$$     POSSIBLE CUSTODIAN TASK TO GET
$$         VECTOR/INTOF,SSURF,...,NORMAL    WORKING
$$
RESERV/P1,6,P2,7,P3,6,P4,5,M,2,X0,11,X1,11,L,11,PT1,11,PT2,11,$
       PT3,11,PT4,11,S,2
P1(1)=POINT/0,0,0
P1(2)=POINT/4.5,0,2.3
P1(3)=POINT/6.5,0,2.8
P1(4)=POINT/8,0,2.8
P1(5)=POINT/9,0,2.3
P1(6)=POINT/10,0,0
P2(1)=POINT/0,5,0
P2(2)=POINT/2,5,1.2
P2(3)=POINT/4,5,1.9
P2(4)=POINT/5,5,2.1
P2(5)=POINT/6.5,5,2
P2(6)=POINT/8,5,1.2
P2(7)=POINT/9,5,0
P3(1)=POINT/0,10,0
P3(2)=POINT/1.5,10,1
P3(3)=POINT/2.5,10,1.3
P3(4)=POINT/3.5,10,1.5
P3(5)=POINT/5,10,1.2
P3(6)=POINT/8,10,0
P4(1)=POINT/0,15,0
P4(2)=POINT/1.5,15,1.5
P4(3)=POINT/3,15,1.5
P4(4)=POINT/5,15,1
P4(5)=POINT/7,15,0
CS1=SCURV/SPLINE,P1(1,THRU,6)
CS2=SCURV/SPLINE,P2(1,THRU,7)
CS3=SCURV/SPLINE,P3(1,THRU,6)
CS4=SCURV/SPLINE,P4(1,THRU,5)
D0=1
D1=.8
X0(1)=0.
X1(1)=0.
I=1
ID1) X0(I)=X0(1)+D0*(I-1)
     X1(I)=X1(1)+D1*(I-1)
L(I)=LINE/X0(I),0,X1(I),10
PT1(I)=POINT/INTOF,L(I),CS1,(PPP=POINT/1,1,1)
PT2(I)=POINT/INTOF,L(I),CS2,PPP
PT3(I)=POINT/INTOF,L(I),CS3,PPP
PT4(I)=POINT/INTOF,L(I),CS4,PPP
I=I+1
IF(I-11) ID1,ID1,ID2
ID2) S(1)=SSURF/SMESH,XYZ,SPLINE,PT1(1,THRU,11),SPLINE,PT2(1,THRU,11),$
                          SPLINE,PT3(1,THRU,11),SPLINE,PT4(1,THRU,11)
PS1=POINT/INTOF,S(1),PARAM,5.1,2.3,0
PS3=POINT/INTOF,S(1),PARAM,.1,.3,26
VS1=VECTOR/INTOF,S(1),PARAM,2.7,1.5,0,NORMAL
VS2=VECTOR/INTOF,S(1),PARAM,2.7,1.5,0,NORMAL,UNIT
VS3=VECTOR/INTOF,S(1),PARAM,2.7,1.5,0,TANSPL
VS4=VECTOR/INTOF,S(1),PARAM,2.7,1.5,0,TANSPL,UNIT
VS5=VECTOR/INTOF,S(1),PARAM,.7,.5,13,CRSSPL
VS6=VECTOR/INTOF,S(1),PARAM,.7,.5,13,CRSSPL,UNIT
M(1)=MATRIX/TRANSL,1,0,0
M(2)=MATRIX/XYROT,30
REFSYS/M(1)
PS4=POINT/INTOF,S(1),PARAM,5.1,2.3,0
REFSYS/NOMORE
REFSYS/M(2)
VS7=VECTOR/INTOF,S(1),PARAM,2.7,1.5,0,NORMAL
REFSYS/NOMORE
CP1=POINT/7,0,0
CP2=POINT/12,5,0
CP3=POINT/15,5,0
CP4=POINT/18,4.3,0
CP5=POINT/21,2,0
CP6=POINT/24,.5,0
CP7=POINT/27,0,0
CP8=POINT/28,0,0
CP9=POINT/32,0,0
CC1=SCURV/CURSEG,CP1,NORMAL,(VECTOR/-5,0,0),CP2
CC2=SCURV/SPLINE,CP2,CP3
CC3=SCURV/COMBIN,CC1,CC2
CV1=VECTOR/INTOF,CC3,PARAM,1,-1,TANSPL
CC4=SCURV/SPLINE,CP3,TANSPL,CV1,CP4,CP5,CP6,CP7
CC5=SCURV/COMBIN,CC3,CC4
CC6=SCURV/SPLINE,CP7,CP9
CC7=SCURV/SPLINE,CP8,CP9
CC8=SCURV/COMBIN,CC5,CC6
CC10=SCURV/SPLINE,CP3,CP4,CP5,NORMAL,(VECTOR/1,1,1),CRSSPL, $
           (VECTOR/0,0,1)
CC11=SCURV/SPLINE,CP5,NORMAL,(VECTOR/1,-1,-1),CRSSPL,(VECTOR/0,1,1),$
            CP6,CP7
CC12=SCURV/COMBIN,CC10,CC11
CV2=VECTOR/INTOF,CC11,PARAM,1,2,NORMAL
CV3=VECTOR/INTOF,CC11,PARAM,1,-1,CRSSPL
CV4=VECTOR/INTOF,CC11,PARAM,1,1,NORMAL
CV6=VECTOR/INTOF,CC11,PARAM,.5,1,BINORM
CV7=VECTOR/INTOF,CC11,PARAM,.5,1,BINORM,UNIT
PRINT/3,ALL
FINI
