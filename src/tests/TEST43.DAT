PARTNO TEST OF SCON/SMIL REGIONAL MILLING
$$ ********************************************
$$ *                                          *
$$ *   THIS TEST OK IN SSV3  -  15-JUN-1989   *
$$ *                                          *
$$ ********************************************
$$
$$   THIS TEST OK IN SSV1 - 08-JUL-1982
$$               AND SSV2 - 05-NOV-1986
$$                          AFTER CHANGING SCON/AXIS,(VECTOR/0,0,1)
$$                                      TO SCON/AXIS,(VECTOR/0,0,1),PLUS
$$                          NEW TRANSLATOR INSISTS ON PLUS BEING PRESENT
$$
CLPRNT
CUTTER/1,.5
MAXDP/.5,8
INTOL/.01
OUTTOL/.01
NUMPTS/30
P1=POINT/0,0,0
P2=POINT/2,0,.1
P3=POINT/3,0,.13
P4=POINT/4,0,0
Q1=POINT/0,1,.3
Q2=POINT/2,1,.5
Q3=POINT/3,1,.4
Q4=POINT/4,1,0.
R1=POINT/0,3,0
R2=POINT/2,3,.2
R3=POINT/3,3,.4
R4=POINT/4,3,.5
S=SSURF/SMESH,XYZ,SPLINE,P1,P2,P3,P4,$
                    SPLINE,Q1,Q2,Q3,Q4,$
                    SPLINE,R1,R2,R3,R4
PA=POINT/0,0,1
PB=POINT/3.9,2.5
C1=SCURV/SPLINE,PA,PB
$$
$$    TEST DS - SCURV - ON CONDITION
$$
SCON/INIT,ALL
SCON/DS,C1,PARAM,0,1,ON,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0,0,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0,0,TANSPL,PLUS
$$
$$    TEST DS - SCURV - AT CONDITION
$$
SCON/INIT,ALL
SCON/DS,C1,PARAM,0,1,AT,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,1,0,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,1,0,TANSPL,MINUS
$$
$$    TEST DS(0.2,1.3) - SCURV - ON CONDITION
$$
SCON/INIT,ALL
SCON/DS,C1,PARAM,0.2,1.3,ON,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0.2,0,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0.2,0,TANSPL,PLUS
SMIL/PATH,DS,PARAM,1.3,0,TANSPL,MINUS
$$
$$    TEST DS(0.2,1.3) - SCURV - AT CONDITION
$$
SCON/INIT,ALL
SCON/DS,C1,PARAM,0.2,1.3,AT,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0.2,0,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0.2,0,TANSPL,PLUS
SMIL/PATH,DS,PARAM,1.3,0,TANSPL,MINUS
$$
$$    TEST PS - ON CONDITION
$$
SCON/INIT,ALL
SCON/DS,C1,PARAM,0,1,ON,(VECTOR/0,0,-1)
SCON/PS,ON,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0,0,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0,0,TANSPL,PLUS
$$
$$    TEST DS - SSURF - ON - TANSPL CONDITION
$$
SCON/INIT,ALL
SCON/DS,S,PARAM,0.3,1.6,0.55,1.8,ON,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0.3,0.55,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0.3,0.55,TANSPL,PLUS
$$
$$    TEST DS - SSURF - AT - TANSPL CONDITION
$$
SCON/INIT,ALL
SCON/DS,S,PARAM,0.3,1.6,0.55,1.8,AT,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,1.4,0.75,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,1.4,0.75,TANSPL,MINUS
$$
$$    TEST DS - SSURF - ON - CRSSPL CONDITION
$$
SCON/INIT,ALL
SCON/DS,S,PARAM,0.3,1.6,0.55,1.8,ON,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0.3,0.55,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0.3,0.55,CRSSPL,PLUS
$$
$$    TEST DS - SSURF - AT - CRSSPL CONDITION
$$
SCON/INIT,ALL
SCON/DS,S,PARAM,0.3,1.6,0.55,1.8,AT,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,(VECTOR/0,0,1),PLUS
SMIL/POSN,DS,PARAM,0.5,1.8,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0.5,1.8,CRSSPL,MINUS
$$
$$     TEST OF ZIGZAG AND PICK FEED
$$
PT1=POINT/0,0,0
PT2=POINT/1,0,.3
PT3=POINT/3,0,0
PT4=POINT/0,1,.3
PT5=POINT/1,1,.5
PT6=POINT/3,1,.3
PT7=POINT/0,3,0
PT8=POINT/1,3,.3
PT9=POINT/3,3,0
SURF=SSURF/SMESH,XYZ,SPLINE,PT1,PT2,PT3,SPLINE,PT4,PT5,PT6,$
                     SPLINE,PT7,PT8,PT9
INTOL/.01
OUTTOL/.01
CUTTER/1,.5
NUMPTS/50
MAXDP/1,10
SCON/INIT,ALL
 SCON/DS,SURF,PARAM,0,2,0,2,ON,(VECTOR/0,0,-1)
SCON/PS,TO,SURF,PLUS,0.
SCON/AXIS,(VECTOR/0,0,1),PLUS
SCON/STEPOV,.05,1.,.1,0.
SCON/FEED,1,2,3,4
SMIL/ZIGZAG,DS,PARAM,0,0,TANSPL,PLUS,STEPOV,PLUS
SCON/FEED,(PLANE/0,0,1,1)
SMIL/PICKFD,DS,PARAM,.1,.1,TANSPL,PLUS,STEPOV,PLUS,1
$$
$$    TEST DS - SSURF - ON - TANSPL CONDITION - MULTAX
$$
MULTAX
SCON/INIT,ALL
SCON/DS,S,PARAM,0.3,1.6,0.55,1.8,ON,(VECTOR/0,0,-1)
SCON/PS,TO,S,PLUS,.1
SCON/AXIS,NORMDS,PLUS
SMIL/POSN,DS,PARAM,0.3,0.55,INCR,(VECTOR/0,0,.5)
SMIL/PATH,DS,PARAM,0.3,0.55,TANSPL,PLUS
FINI
