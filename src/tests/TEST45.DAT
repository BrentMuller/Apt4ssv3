PARTNO TEST45 3-CORNER PATCHES AND IMPROVED SPLINING TEST
$$ ********************************************
$$ *                                          *
$$ *   THIS TEST OK IN SSV3  -  16-JUN-1989   *
$$ *                                          *
$$ *   THOUGH CONTAINS VALID WARNING 5129     *
$$ *                                          *
$$ ********************************************
$$
$$   THIS TEST OK IN SSV1 - 08-JUL-1982
$$               AND SSV2 - 05-NOV-1986
$$
CLPRNT
PRINT/TABPRT,ON
REMARK DEFINE AND MILL GENCUR SURFACE WITH NULL CURVE(3 CORNER PATCH)
PA=POINT/0.0, 0.0, 0.0
PB=POINT/1.0, 0.0, 0.4
PC=POINT/2.0, 0.0, 0.4
PD=POINT/3.0, 0.0, 0.0
$$
CURVA=SCURV/SPLINE,PA,PB,PC,PD,FLOW,ARC,0,3, $
            SEG,LENGTH,0,1,LENGTH,1,2,LENGTH,2,3
$$
QA=POINT/0.0, 1.0, 0.0
QB=POINT/1.3, 1.0, 0.3
QC=POINT/1.7, 1.0, 0.3
QD=POINT/3.0, 1.0, 0.0
$$
CURVB=SCURV/SPLINE,QA,QB,QC,QD,FLOW,ARC,0,3, $
            SEG,LENGTH,0,1,LENGTH,1,2,LENGTH,2,3
$$ LAST SECTION INCLUDES NULL CURVE
RA=POINT/0.0, 2.0, 0.0
RB=POINT/1.5, 2.0, 0.25
RC=POINT/3.0, 2.0, 0.0
VB=VECTOR/1,0,0
$$  DEFINE 3 INDIVIDUAL CURVES - THEN COMBINE THEM
C1=SCURV/SPLINE,RA,RB,TANSPL,VB
$$  NULL CURVE IS DEFINED NEXT
CNULL=SCURV/SPLINE,RB,RB
C3=SCURV/SPLINE,RB,TANSPL,VB,RC
$$
CURVC=SCURV/COMBIN,C1,CNULL,C3,FLOW,ARC,0,1,$
                                    PARAM,1,2, $ (NULL CURVE)
                                    ARC,2,3, $
                                SEG,LENGTH,0,1, $
                                    PARAM,1,2,  $ (NULL CURVE)
                                    LENGTH,2,3
$$ DEFINE SURFACE
MAXDP/-4,0
$$   FIRST CASE GENERAL SPLINE ONLY GENCUR SURFACE
SURF3=SSURF/GENCUR,CURVA,CURVB,CURVC
$$   SECOND CASE - RULED GENCUR SURFACE
SURF=SSURF/GENCUR,CURVA,CURVC
$$
MAXDP/-4,0
$$ MILL SURFACE
MULTAX
CUTTER/1,0.5
INTOL/.01
OUTTOL/.01
MAXDP/1,10
SCON/INIT,ALL
SCON/DS,SURF,PARAM,0,3,0,.75,ON,(VECTOR/0,0,-1)
SCON/PS,TO,SURF,PLUS,0.0
SCON/FEED,1,2,3,4
SCON/STEPOV,.07,1,0,0
SCON/AXIS,NORMDS,PLUS
SMIL/PATH,DS,PARAM,0,.5,TANSPL,PLUS
REMARK DEFINE A BOTTLE PROFILE TWICE, ONCE WITH OLDER AND
REMARK NEXT WITH AN IMPROVED SPLINING TECHNIQUE
PP1=POINT/ 0.0, 1.125
PP2=POINT/ 1.0, 1.060
PP3=POINT/ 2.0, 0.900
PP4=POINT/ 3.0, 1.000
PP5=POINT/ 5.0, 1.125
PP6=POINT/ 7.0, 0.875
PP7=POINT/ 9.0, 0.400
PP8=POINT/10.0, 0.380
VEND=VECTOR/1,0,0
$$  FIRST FIT OLD CURVE THRU POINTS
OLDFIT=SCURV/SPLINE,PP1,TANSPL,VEND,PP2,PP3,PP4,PP5,PP6, $
        PP7,PP8,TANSPL,VEND
$$  SWITCH TO NEW SPLINING METHOD BY FOLLOWING MAXDP CALL
MAXDP/-4,10
NEWFIT=SCURV/SPLINE,PP1,TANSPL,VEND,PP2,PP3,PP4,PP5,PP6, $
        PP7,PP8,TANSPL,VEND
$$  FINALLY REVERT BACK TO OLD SPLINE FIT BY A MAXDP CALL
MAXDP/-4,0
$$
$$
REMARK THE FOLLOWING TESTS DEMONSTRATE THAT A THREE CORNER PATCH
REMARK CAN BE DEFINED BY REVOLV AND CROSS DEFINITIONS
$$  DEFINE CIRCULAR ARC STARTING AT ORIGIN
PT1=POINT/ 0.0, 0.0, 0.0
PT2=POINT/ 2.0, 0.0, 0.7
PT3=POINT/ 4.0, 0.0, 0.4
$$
CIRC=SCURV/CURSEG,PT1,PT2,PT3
$$  DEFINE SURFACE BY REVOLVING CIRC AROUND AXIS
$$  THE AXIS ALSO PASSES THRU ORIGIN - THUS MAKING A 3 CORNER PATCH
PAX1=POINT/ 0.0, 0.0, 0.0
PAX2=POINT/ 4.0, 0.0, 0.0
SCIRC=SSURF/REVOLV,CIRC,AXIS,PAX1,PAX2,CLW,0,270
$$
REMARK USE THE CROSS DEFINITION TO DEFINE A RIGHT CIRCULAR CONE
REMARK WITH APEX.  THE APEX OF THE CONE IS THE COLLAPSING CORNER
REMARK OF THE RESULTING PATCH STRUCTURE
$$
$$  DEFINE A CIRCULAR CROSS SECTION CURVE FOR THE CONE
TA=POINT/ 1.0, 1.0, 0.0
TB=POINT/ 1.0, 0.707, 0.707
TC=POINT/ 1.0, 0.0, 1.0
$$
CIRSEC=SCURV/CURSEG,TA,TB,TC
$$  DEFINE A SCALE CURVE FROM THE ORIGIN TO 4,4,4
$$  THE USE OF THE ORIGIN CAUSES THE APEX OF THE CONE TO BE
$$  INCLUDED AS A BOUNDARY CURVE
SCLA=POINT/0.0, 0.0, 0.0
SCLB=POINT/4.0, 4.0, 4.0
CRVSCL=SCURV/SPLINE,SCLA,SCLB
$$ CONE DEFINITION
CONSRF=SSURF/SCALE,CIRSEC,CROSS,CRVSCL
$$
FINI
